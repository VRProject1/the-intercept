// Символьная переменная. Мы отслеживаем только два, используя шкалу +/- 
VAR forceful = 0
VAR evasive = 0


// Инвентарь
VAR teacup = false
VAR gotcomponent = false


// Состояния истории: это может быть сделано с помощью считывания количества узлов; или функций, которые собирают более сложную логику; или переменных
VAR drugged = false
VAR hooper_mentioned = false

VAR losttemper = false
VAR admitblackmail = false

// какую подсказку мы передали Хуперу?
CONST NONE = 0
CONST STRAIGHT = 1
CONST CHESS = 2
CONST CROSSWORD = 3
VAR hooperClueType = NONE

VAR hooperConfessed = false

CONST SHOE = 1
CONST BUCKET = 2
VAR smashingWindowItem = NONE

VAR notraitor = false
VAR revealedhooperasculprit = false
VAR smashedglass = false
VAR muddyshoes = false

VAR framedhooper = false

// Что вы сделали с этим компонентом?
VAR putcomponentintent = false
VAR throwncomponentaway = false
VAR piecereturned = false
VAR longgrasshooperframe = false


// Режим отладки добавляет несколько ярлыков - не забудьте установить значение false в релизе!
VAR DEBUG = false
{DEBUG:
	IN DEBUG MODE!
	*	[Beginning...]	-> start
	*	[Framing Hooper...] -> claim_hooper_took_component
	*	[In with Hooper...] -> inside_hoopers_hut
- else:
	// Первая диверсис чего мы начнем?
 -> start
}

 /*--------------------------------------------------------------------------------
	Завершите движение персонажа с помощью функций, если мы захотим развить эту логику в будущем
--------------------------------------------------------------------------------*/


 === function lower(ref x)
 	~ x = x - 1

 === function raise(ref x)
 	~ x = x + 1

/*--------------------------------------------------------------------------------

	Начинайте рассказ!

--------------------------------------------------------------------------------*/

=== start === 

//  Вступление
		<i>Моё имя и деятельность засекречны, по крайней мере для тех, кто знать это не должен.
	    <i>Меня посадили под арест в одну из Хижин военного комплекса бог знает где. Меня ввели вовнутрь хижины, посадилм, и закрыли дверь.
    	<i>Сейчас единственное, что мне остаётся ждать кого-то из главных этой военной базы, и им надо решить "проблему" на своей территории, пока всё не вышло из под контроля не полетели с плеч невинные головы.
		* [Это Хижина 14.] 
		Когда я сел, дверь была заперта. 
		<i>Здесь нет ничего, что бы я мог продолжить свою работу, только стены, пол, потолок одно маленькое окошко, стол и стул. И всё. В кармане у меня лежит утренняя перехваченная шифровка, но от одного взгляда на перепутанные буквы я просто схожу с ума. 
		<i>Я не машина, что бы обо мне ни говорили.

	- (opts)
		{|<i>Я стучу пальцами по столу.</i>|}
 		* 	(think) [Подумать о перспективе грядущих событий ...] 
 			<i>Они подозревают меня в предательстве. Они думают, что я украл компонент из счетной машины. Они обыщут мою койку и чемоданы. 
			<i>Когда они не найдут его, {plan:тогда} они вернутся и потребуют, чтобы я говорил. </i>
			-> opts
 		*	(plan) [Обдумать план действий ...]
 			<i>{not think:Я тот, кто |Я человек, который} не церемонится с проблемами. Хорошо разбирается в цифрах, быстро разгадывает кроссворды, отлично играет в шахматы. 
 			<i>Но в этом сценарии — в этой ловушке — что является выигрышной игрой?
 			* * 	(cooperate) [Сотрудничать в расследовании ...] 
	 				<i>Я должен сотрудничать. Мой авторитет-это мой главный актив. Противоречить самому себе или другому источнику было бы фатально. 
	 				<i>Я просто надеюсь, что они не зададут вопросов, на которые я не хочу отвечать.
		 			~ lower(forceful)
	 		* * 	[Повести по ложному следу ...] 
		 			<i>Значит, дезинформация. Точно так же, как Война в Европе-это война ставок и шифровок, а не самолетов и бомб.
                    <i>Моя лучшая надежда-это история, которую они предпочитают правде. 
		 			~ raise(forceful)
	 		* * 	(delay) [Уклоняйся и Молчи ...] 
		 			<i>Уклонение и задержка. Военная машина никогда не сражается на одном фронте. Если я буду действовать достаточно медленно, все разрешится как-нибудь иначе, и моя репутация останется нетронутой.
		 			~ raise(evasive)
		*	[Ждать дальше ...]		
	- 	-> waited

= waited 
	-	Проходит полчаса, прежде чем возвращается Командир Гаррис. Он быстро закрывает за собой дверь, как будто боится, что в нее может проскользнуть неосторожное слово.
		- <b>"Ну, тогда ... "</b> - неловко начинает он. <b>"Произошёл инцидент."</b>
		*	<b>"Командир!"</b>
			Он кивнул. <>
		*	(tellme) {not start.delay} <b>"Расскажите мне, в чем дело. "</b>
			Он отрицательно качает головой. 
			<b>"А теперь давай не будем притворяться."</b>
		*	[Промолчать ...]
			<i>Я ничего не говорю.
	-	Он принес две кружки с горячим чаем и поставил их на стол.
		*	{tellme} [Отрицать ...] <b>"Я не притворяюсь."</b>
			{cooperate:<i>Я уже лгу, несмотря на все свои добрые намерения.}
			Гаррис смотрит неодобрительно. -> pushes_cup
		*	(took) [Взять кружку ...]
			~ teacup = true
			Я беру кружку и грею руки. Это <>
		*	(what2) {not tellme} <b>"Что происходит?"</b>
			Гаррис: <b>"Ты уже знаешь! Вопросы здесь задаю я!"</b>
			-> pushes_cup
		*	[Ждать ...]
			Я жду, когда он заговорит.
			- - (pushes_cup) Он наполовину пододвигает ко мне одну кружку: <>
	-	словно это небольшой дружеский жест.
		<i>Достаточно, чтобы дать мне надежду?</i>
 		* 	(lift_up_cup) {not teacup} [Взять кружку ...] 
 				Я {took:поднял кружку|взял кружку,} и сдул струйки пара. Слишком горячо чтобы пить. 
 				Гаррис берет свою чашку и просто держит её.
 				~ teacup = true
 				~ lower(forceful)
 		* 	{not teacup} [Не брать чашку ...] 
 				<i> Это даже на чай не похоже. Просто коричневая жижа, замаскированная под чай.
 				~ raise(forceful)
				
		*	{teacup} 	[Выпить ...] 
				Я подношу чашку ко рту, но она слишком горячая, чтобы пить.

		*	{teacup} 	[Выжидать ...] 		
			Я ничего не говорю -> lift_up_cup

- 	<b>"Довольно сложная ситуация,"</b> - начинает {lift_up_cup:он|Гаррис} {forceful <= 0:, строго}. 
    <i>Я и раньше видел, как он принимает этот жесткий тон голоса, но только когда разговаривает с начальством.</i> 
    <b>"Я уверен, что вы согласны."</b>
 		* 	[Согласиться ...] 
 				<b>"Соглашусь, ситуация неоднозначная."</b> - я отвечаю
 		* 	(disagree) [Не согласиться ...] 
 				<b>"Я не совсем понимаю что вы имеете в виду,"</b> - говорю я
				 ~ raise(forceful)
				 ~ raise(evasive)
 		* 	[Солгать ...] -> disagree
 		* 	[Уклониться ...] 
 				<b>"Я уверен, что вы справлялись и с худшим,"</b> - говоря небрежно
 				~ raise(evasive)
	- 	{ teacup:
 			~ drugged  = true
			<>, я потягиваю чай, как будто мы старые друзья.
 	  	}
		<>.
 		
 	-
 		*	[Посмотреть на него ...]
			<i>Его лицо ничего мне не говорит. Я раньше видел Гарриса более раскрытым и полным смеха. Сегодня он замкнут, став такой же частью военной машины, как и устройство в хижине № 5.

 		*	[Ожидать ...]
 			Я жду, как он отреагирует.

 		*	{not disagree} [Улыбнуться ...]
 			<i>Я пытаюсь слабо улыбнуться. Он всё также холоден.
 			~ lower(forceful)
		
// Почему ты здесь
	- 	
		<b>"Нам нужна эта запчасть,"</b> - говорит Гаррис.
		
	-	//<b>"Альтернативы, конечно, нет,"</b> - он продолжает:
		{not missing_reel:
			-> missing_reel -> harris_demands_component
		}
	-	
 		* 	[Да]
 			<b>"Конечно же я знаю,"</b> - я отвечаю. 
 		* (no) [Нет]
 			<b>"Нет, и у меня есть работа..."</b>
			<b>"Работа, которая будет довольно трудной для вас, не так ли?"</b> - Гаррис перебивает:. 
			
 		* 	[Увернуться ...]
 				-> here_at_bletchley_diversion
 		* 	[Солгать ...] 
 				-> no
 	-	-> missing_reel -> harris_demands_component

=== missing_reel === 
	*	[Украденный компонент...]
	*	[Пожать плечами]
		Я пожимаю плечами. 
		->->
	- 	Сегодня днём пропала катушка с бомбы. Мы вчетвером работали в хижине, трудились над последним перехваченным сообщением немецкой армии. Результаты были просто никудышными. Именно Рассел нашел зазор в плите. 
	- Любой из нас мог бы взять его, и никто другой не знал бы его ценности.
 	
 		*	{forceful <= 0 }[Поддаться панике ...] Они повесят это на меня. Им нужен козел отпущения, чтобы работа могла продолжаться. Я - вероятная цель. Слабее всех остальных. 
 			~ lower(forceful)
 		*	[Подсчитать ... ] Значит, мои шансы-один к четырем. Неплохо, хотя сами ставки выше, чем мне бы хотелось.
 			~ raise(evasive)
 		*	{evasive >= 0} [Отвергнуть ...] Но это все еще простая формальность и работа просто так не остановится. Будет произведена замена компонентов, и мы все вернемся к работе. Мы слишком ценны, чтобы быть расстреляными.
 			~ raise(forceful)
 	-	->->


=== here_at_bletchley_diversion
	<b>"Здесь, в Блетчли? Конечно."</b>
 	~ raise(evasive)
 	~ lower(forceful)
	<b>"Вот, сейчас, - поправляет Гаррис. - Мы не со всеми разговариваем. Я могу себе представить, что вы можете чувствовать себя довольно обиженным из-за этого. Я могу себе представить, что ты чувствуешь себя задетой. { forceful < 0:Ты очень ранимая душа.}<b>"

 	* (fine) <b>"Я в порядке[."</b>],"</b> - говорю я. <b>"Все это какое-то недоразумение, и чем скорее мы его проясним, тем лучше."</b>
 		~ lower(forceful)
		<b>"Я не могу не согласиться."</b> - А потом он прямо заявляет об этом, с обвинением.
	
	*	{forceful < 0}	<b>"Что вы хотите этим сказать?"</b>

 	* (sore) { forceful >= 0 } <b>"Чертовски прав[."</b>] Мне обидно. Это кто-то из тех, кто тебя подговорил? Это был Хупер? Он всегда мне завидовал. Он..."</b>
 		~ raise(forceful)
 		~ hooper_mentioned = true
		Капитан ощетинился и поджал губы. - А теперь есть? О своих достижениях, как вы думаете?"</b> - 
 		Трудно не избавиться от ощущения, что он ...  { evasive > 1 :издевается|смеётся} надо мной.
		<b>"Или твоего мозга? Или что-то еще?"</b>
 		* * 	<b>"Моей гениальности.["] Хупер просто не может смириться с тем, что я умнее его. Мы так тесно работаем вместе, целыми днями сидим взаперти в этой хижине. Это сводит его с ума."</b>
				<b>"Вы предполагаете, что Хупер будет подрывать будущее этой страны просто назло вам всем?"</b> - Гаррис выбирает слова, как военный, каждый из которых выстраивается в кольцо вокруг меня.
 					* * * 	[<b>"Да!"</b>] 			
	 							<b>"{ forceful > 0:Конечно, он достаточно мелок|Я бы не оставил это без внимания}. Он мерзавец."</b> - { teacup : Я поставил чашку на стол.|Я вытираю рукой лоб.}
	 							~ raise(forceful)
	 							~ teacup = false
 					* * * 	[<b>"Нет!"</b>] 			
	 							<b>"Нет, { forceful >0:конечно нет|подразумеваю, что нет}."</b> - { teacup :Я поставил чашку на обратно на стол.|Я двигаю чашку за основание}. 
							 	~ lower(forceful)
								~ teacup = false
 					* * * 	[Ускользнуть от ответа ...] 		
	 							<b>"Я не знаю, что я предлагаю. Я не понимаю, что происходит."</b>
	 							~ raise(evasive)
								<b>"Но ты, конечно, знаешь<b>" Гаррис прищуривается. 
								-> done

					- - - 	(suggest_its_a_lie) <b>"Все, что я могу сказать, это то, что с тех пор, как я приехал сюда, он искал способы сбить меня с толку. Я не удивлюсь, если он все это затеял только для того, чтобы отдать меня под трибунал."</b>
							<b>"Мы не отдаем гражданских под трибунал<b>" — отвечает Гаррис. <b>"Предателей просто вешают по желанию ее величества."</b>
 					* * * 	<b>"Совершенно верно[."</b>],"</b> - отвечаю бойко.
 					* * * 	(iamnotraitor) <b>"Я не предатель[."</b>],"</b>Отвечая {forceful > 0 :энергично|, дрожащим голосом. <b>"Ради бога!"</b>}
 					* * * 	[Солгать ...] -> iamnotraitor
					- - - Он смотрит. 

 		* * 	<b>"О моем положении["] Моя репутация."</b> - { forceful > 0:Я понимаю, как высокомерно это звучит, но все равно продолжаю работать.|Я не люблю так говорить о себе, но все равно продолжаю.} <b>"Хуперу просто невыносимо сознавать, что, когда все это закончится, рыцарство будет принадлежать мне, а не ему..."</b>
				<b>"Никто не получит рыцарского звания, если немцы высадятся на берег,"</b> - резко отвечает Гаррис. Он бросает быстрый взгляд на дверь хижины, чтобы проверить, что задвижка все еще опущена, затем продолжает более шепотом: <b>"А теперь ответь мне."</b> - 
				В первый раз с тех пор, как закрылась дверь, я задаюсь вопросом, какая угроза может быть, если я это <i>не</i> сделаю. 
 				
 		* * 	[Уклониться ...] 				
 				~ teacup = false
 				~ raise(forceful)
 				<b>"Откуда мне знать?"</b> - Отвечаю я, защищаясь. { teacup :Я поставил чашку обратно на стол.}  -> suggest_its_a_lie
 				

 	* [Быть честным ...] 	-> sore
 	* [Обмануть ...] 	-> fine

-	(done) -> harris_demands_component


=== harris_demands_component ===
	<b>"{here_at_bletchley_diversion:Пожалуйста|И так}. Запчать у тебя?"</b> - Гаррис {forceful > 3:слегка вспотел|не тратит время зря}: Блетчли-это его часы.. <b>"Ты знаешь где она?"</b>
	 	* 	[<b>"Да!"</b>]
	 		<b>"Знаю."</b> - 
	 		-> admitted_to_something
	 	* (nope) [<b>"Нет!"</b>] <b>"Не имею понятия где она может быть."</b> - 
	 					-> silence
	 	* [Солгать ...] 		-> nope
	 	* [Уклониться от ответа ...] 		
	 		<b>"Запчасть?"</b>
			 ~ raise(evasive)
			 ~ lower(forceful)
			<b>"Не глупи,"</b> - говорит капитан. <b>"{ not missing_reel:Деталь которая пропала сегодня днем.} Где она?"</b>
			
	- 	{ not missing_reel:
			-> missing_reel -> 
		}
 		* 	[Сотрудничать ...] <b>"Я знаю где она."</b>
 			-> admitted_to_something
 		* (nothing) [Затаиться ...] <b>"Я ничего об этом не знаю."</b> - Мой голос дрожит{ forceful > 0:  со злобой|; Я не привык сталкиваться лицом к лицу с людьми у которых есть оружие}. 

 		* [Солгать ...] -> nothing
 		* [Уклониться ...] 

			<b>"Я не знаю, что дает вам право придираться ко мне. { forceful > 0:Я требую адвоката.|Я хочу увидеть адвоката.}<b>"

			<b>"Это военное время,"</b> - говорит капитан Гаррис. <b>"И клянусь Богом, если мне придётся пристрелить тебя что бы найти деталь, то я сделаю это. Ты меня понял?"</b> - Он показывает на кружку,-> drinkit

		-	(silence) Возникла звонкая тишина. { forceful > 2:Его броня дала трещину.|{ evasive > 2:Он устал от моей уклончивости.}} 

		// Пей чай и говори
		- (drinkit) <b>"А теперь пей чай и говори."</b>
		 * { teacup  }   	[Пить чай ...] 			-> drinkfromcup
		 * { teacup  }   	[Поставить чашку на стол ...] 
		 		Я снова осторожно поставил чашку на стол.
				~ teacup = false
				~ raise(forceful)
				-> whatsinit
		
		 * { not teacup  }  [Взять кружку] 
		 		- - (drinkfromcup) Я поднял кружку { teacup :к губам }и хлебнул. Он ждет, пока я сглотну, прежде чем снова заговорить.
			 		~ drugged  = true
			 		~ teacup    = true
		 * { not teacup  }  [Не брать кружку...] 
		 		Я игнорирую, и не прикосаюсь к чашке. 
				~ raise(forceful)
				- - (whatsinit) <b>"Вы явно принуждаете выпить содержимое чашки!"</b> - Затем я спрашиваю - <b>"Что в чашке?"</b>
				
	- 	<b>"Лапсанг Сушонг,"</b> - {drinkfromcup:подмечает|отвечает} он, он поставил свою нетронутую чашку обратно на стол. <b>"Такой странный вкус. Может быть, это и не чай вовсе. Можно сказать, что она скрывает множество грехов. Как и ты. Разве не так?"</b>
 
		 * (suppose_i_have) [Согласиться с наличием скрытых грешков ...] 
			 	// Сожалеть о поступке
				<b>"Полагаю, что да,"</b> - отвечаю. <b>"Я делал вещи которых не должен был делать."</b> - 
				 ~ lower(forceful)
				 -> harris_presses_for_details

		* (nothing_ashamed_of) { not drugged  }   [Не соглашаться в своей греховности ...]
		 		<b>"Я не делал ничего такого, за что мне должно быть стыдно!"</b>
 				-> harris_asks_for_theory

		 * (cant_talk_right) { drugged  }   [Не соглашаться в своей греховности ...] 
			 	Я открываю рот, чтобы возразить, но слова, которые я хочу, не приходят. Это похоже на то, как если бы Гаррис вставил отвертку мне в зубы и заставил её держать.
 				-> admitted_to_something.ive_done_things

 		 * {drugged} [Соврать ...] 	-> cant_talk_right
 		 * {not drugged} [Соврать ...] 	-> nothing_ashamed_of
		 * { drugged  }   [Уклониться от ответа ...] -> cant_talk_right
		 * { not drugged  }   [Уклониться от ответа ....] 
		 		<b>"Никто из нас не безупречен, Гаррис.{ forceful > 1:И ты не мой священник и я не твой|Но я ничего не сделал, чтобы заслужить такое обращение.}. А теперь, пожалуйста отпусти меня. И я помогу тебе найти этот проклятый компонент."</b> - 
				//   Кого винить?
				Похоже, он обдумывает это предложение. 
				 -> harris_asks_for_theory



=== harris_presses_for_details
// Открыто шантажировать
	<b>"Ты хочешь сказать, что настольк оне дальновиден, что позволил себе быть уязвимым к давлению и стрессу?"</b> - Harris answers. <b>"И не говори что это не так! Ты теперь это тоже должен понимать!"</b>
	 	* [<b>"Да!"</b>] -> admit_open_to_pressure
	 	* { not drugged  } [<b>"Нет!"</b>] 
	 			"<b>Я ничего такого не говорю!"</b> - огрызаюсь я в ответ. <b>"Что происходит, Гаррис? Вы обвиняете меня в предательстве, но но сами не предоставили ни одного доказательства своих "громких слов"! Почему бы Вам уже не выложить свои карты на стол?"</b>
			 	~ raise(forceful)
			 	
				
	 	* {drugged} [<b>"Нет!"</b>] 
	 			<i>Я яростно качаю головой, чтобы сказать: нет, это не так, но что бы ни было не так с языком, то не так и с шеей. Я смотрю через стол на лицо Гарриса и вдруг понимаю, как он мне сочувствует. Такой добрый, щедрый человек. Как я могу что-то скрывать от него?
			 	~ lower(forceful)
				<i>Прежде чем ответить, я делаю еще один глоток горького чая со странным вкусом.
			 	-> admit_open_to_pressure


	 	* { not drugged  } [Уклониться что не отвечать прямо ...] 
	 			<b>"Это Вы, кто оказывает давление здесь!"</b> - отвечаю я { forceful > 1:изящно|несколько лицемерно}. <b>"Я просто жду, пока Вы не скажете мне, что происходит на самом деле!"</b>
				 ~ raise(evasive)
	 	* { drugged  } [Уклониться ...] 				 
	 			<b>"Мы все здесь находимся под давлением."</b>
	 			<i>Он посмотрел на меня с жалостью. -> harris_has_seen_it_before

 	-	<b>"Это достаточно просто,"</b> - сказал Гаррис. -> harris_has_seen_it_before

= admit_open_to_pressure
	<b>"Это действительно так,"</b> - отвечаю я. <b>"Есть некоторые вещи... чего мужчине делать не следует."</b>
	 ~ admitblackmail  = true
	<i>Гаррис оставался спокойным, и особо не приближался, как будто мое состояние может быть заразным. Я думал, что в армии их учат стрелять в таких, как я, с первого взгляда. 
	<i>Он тоже не выражает сочувствия. Он кивает один раз. Он видит меня всего лишь как вращающийся винтик в его расчетах, который в целом не имеет никакого тактически важного значения .
 	-> harris_has_seen_it_before


=== admitted_to_something
	// Признаться В Чем-То
	{ not drugged  :
		<i>Гаррис уже долго смотрит на меня не отвлекаясь ни на что другое. { evasive == 0:Он не ожидал, что меня так легко сломить..}
	- else:
		<i>Гаррис удовлетворенно улыбается, как будто моя готовность говорить была его заслугой.
	}
	<b>"Ясно."</b>
	Возникает долгая пауза, похожая на задержку между подачей линии шифра в бомбу и ожиданием, пока ее клапаны прогреются достаточно, чтобы начать обработку.
	<b>"Может... Желаешь сам дать объяснение?"</b>
		 * 	[Объяснить ...] 
		 	<i>Я замолкаю на мгновение, пытаясь подобрать слова. Просто выйти из тени и начать говорить об этом после того, как всю жизнь прятался... это всё равно что круг, который я не могу сделать квадратным.
		 	* * 	[Объяснить содеянное ...] 	-> ive_done_things
		 	* * 	{drugged} [Промолчать ...] 	-> say_nothing
		 	* * 	{not drugged} [Солгать ...] 	-> claim_hooper_took_component

		 * { not drugged  }   [Ничего не объяснять ...]
		 		<b>"- Тут нечего объяснять!"</b> - натянуто отвечаю я. -> i_know_where
				
		 * { not drugged  }   [Солгать ...] -> claim_hooper_took_component
		 * { not drugged  }   [Уклониться ...]
		 	<b>"Объяснить вам, что вы должны делать, вместо того, чтобы запугивать меня? Конечно!"</b> - Я складываю руки на груди. -> i_know_where

		 * (say_nothing) { drugged  }   [Ничего не говорить ...]
		 	<i>Я скрещиваю руки на груди, твердо намереваясь ничего не говорить. Но почему-то, глядя на лицо Гарриса, я не могу заставить себя сделать это. Я хочу признаться. Я хочу рассказать ему все, что смогу, объясниться с ним, заслужить его прощение. Это ощущение настолько сильно, что моя воля бессильна перед ним.
			<i>Со мной что-то не так, я уверен в этом. На языке у меня странный горьковатый привкус. Я чувствую его, когда начинают складываться слова.
		 	-> ive_done_things

= i_know_where
	<b>"Я знаю, где находится ваш компонент... Это просто потому, что это так очевидно, где он вообще может находится. Но это не значит, что взял его я, из-за того, что только я могу решать проблемы такого рода. И как это не значит того, что я немецкий шпион, потому что я могу взломать их коды."</b>
	-> harris_asks_for_theory


= ive_done_things
	 <b>"Я совершал разные вещи,"</b> - рассказываю я {harris_demands_component.cant_talk_right: совершенно безнадежно}. <b>"Вещи, которые я не хотел делать. Я старался этого не делать. Но в конце концов, это было похоже на то, чтобы отрезать себе руку, чтобы сопротивляться."</b>
	-> harris_presses_for_details




=== harris_asks_for_theory
<b>"Тогда расскажи мне,"</b> - просит Гаррис. <b>"Какова твоя теория? Ты славный малый — такой же смышленный, как и все остальные кто здесь есть, и это о чем-то говорит. Каково Твоё мнение о пропавшем компоненте? Может быть, несчастный случай? Или ты винишь кого-то из других мужчин? { hooper_mentioned :Может быть лейтенанта Хупера?}"</b>
 	* [Не овинять других ...] 
 		-> an_accident
 	* [Обвинить кого-то другого ...] -> claim_hooper_took_component

= an_accident
	<b>"Это просто случайность, определённо!"</b> - Я рискнул улыбнуться. <b>"Эта проклятая машина сделана из запасных частей и веревки. Даже эти хижины протекают, когда идет дождь. Не потребуется больше одного человека, чтобы споткнуться о кабель, чтобы компонент выпал из машины. Ты не пробовал заглянуть вот туда?"</b>
	<b>"Вы верите, что я этого не делал!?"</b>
	<i>Внезапно я понимаю, что его ответ - это угроза.
	<b>"А сейчас,"</b> - продолжил он. <b>"Ты уверен, что ничего не хочешь мне рассказать?"</b>

	 * [Сотрудничать со следствием ...]
	 	<b>"Всё в порядке."</b> - Со вздохом моё непоколебимость рушится. <b>"Если вы обыскали мои вещи, то я полагаю, что вы нашли { evasive > 1: то, что вам необходимо|мои письма. Разве не находили? Если на самом деле вы этого не делали, не говорите мне}."</b>
		 ~ admitblackmail  = true
		Гаррис кивнул. 
		<> -> harris_has_seen_it_before

	 * {evasive > 0} [Уклониться ...] <b>"Только то, что Вы ведёте себя неразумно, и что больше похожи на дикого кабана, которому повезло найти какую-то редкую сладость."</b>
		// Теряет самообладание
		<b>"Ты дегенерат!"</b> - неожиданно громко и яростно отвечает Гаррис. Он наполовину поднялся со стула. <b>"Если ты знаешь ситуацию так же как и я, то почему это притворство?! Гунны стоят наготове, как крысы, готовые разбежаться по всей стране. Они всё уничтожат. Ты ведь понимаешь это, не так ли? Вы же не настолько заперты в своих кроссвордах, чтобы не замечать этого, не так ли? Эта машина, которую мы имеем здесь — вы, люди, — Вы лучшая и единственная надежда этой страны. Да поможет ей бог."</b>
			~ losttemper  = true
			<i>Я откидываюсь назад, пораженный силой его гнева. Его тщательно вылепленное выражение лица исказилось до гневного отвращения. Он открыто ненавидит меня, мне кажется. Он выпьет всю мою кровь что бы утихомирить свой гнев в отношении моего предательства.
		* * [Успокоить командира ...]
			<b>"Держите себя в руках,"</b> - говорю я, жестикулируя, чтобы он успокоился.
	 		
		* * [Подразнить ...] 
			<b>"Я могу себе представить, что значит это быть окруженным умными людьми которые являются скрытой угрозой для вас, Командир"</b> - Отвечаю я с усмешкой. <b>"В Вооруженных силах вас не учат думать."</b>
			 ~ raise(forceful)
			
		* * [Отмахнуться ...]
			<b>"А потом я продолжу свою работу по ее спасению, не так ли?"</b> - Я даже приподнимаюсь на ноги, прежде чем он хлопает по столу.
			
		- - <b>"Говори!"</b> - настаивает Грарис. <b>"Говори сейчас же!. Скажи мне, где ты его спрятал или кому передал. Или, Бог мне судья, я разорву твое жалкое тельце на куски, разыскивая его."</b>
	 		-> harris_demands_you_speak




=== harris_has_seen_it_before
	<b>"Я видел это раньше. Такой молодой человек, как ты - умный, удаленный. Тот, который не ходит на вечеринки. Кто слишком серьезно относится к себе. Кто заходит слишком далеко."</b>
	Он скользит большим пальцем между двумя пальцами.
	<b>"Теперь они владеют тобой."</b>
	
	 * [Согласиться и сказать ...] 
	 	<b>"Что я мог сделать?"</b> - Меня сейчас трясет. Ночь холодная, и инфракрасная лампа в Хижине убрана. <b>"{ forceful > 2:Я не буду|Я не хочу} сесть в тюрьму."</b>
	 	<b>"А ты не дурак."</b> - he replies. <b>"Ты там и недели не продержишься.		
		
	 * [Выкрутится из ситуации ...] 
		 <b>"Я все еще могу это исправить."</b>
		Гаррис качает головой. <b>"Ты ничего не будешь делать. Это выше твоих возможностей. Ты можешь попасть в тюрьму или пойти на расстрел - или мы можем изменить твое имя и перевезти тебя туда, где твои неблагоразумие не причинят тебе вреда. Но прямо сейчас ничего из этого не имеет значения. Что произойдёт с тобой не имеет значения. Важно только, где находится этот компонент.
		
	 * { not drugged  }   [Соврать ...] 
	 	<b>"Я Вам расскажу!"</b>, - говорю я ему. <b>"Я думаю, что смогу узнать, кто они такие. Отведу вас к ним"</b>.
		Гаррис смотрит на меня с презрением. <b>"Ты негодяй. Ты заплатишь за то, что ты сделал с этой страной сегодня. Если одинокий мужчина потеряет свою жизнь из-за твоей гордости и твоих извращений, тогда только Бог поможет твоей душе. 

	*  {drugged} {forceful < 0} [Извиниться в содеянном ...]
		<b>"Гаррис, я ..."</b>
		~lower(forceful)
		<b>"Прекрати!"</b>, - прерывает он. <b>"Здесь нет присяжных, чтобы раскаиваться. И нет времени для соплей!"</b>

- 	(tell_me_now) <> <b>"Так почему бы тебе не сказать мне прямо сейчас. Где это находится?"</b>
	-> harris_demands_you_speak




=== harris_demands_you_speak
	Его глаза смотрят вниз, как обугленные сверла.
 * [Confess] 
	 	{ forceful > 1 :
		<b>"Хочешь, что бы я рассказал тебе что случилось? Ты почувствуешь отвращение!"</b>
		-else:
			<b>"Хорошо. Я расскажу тебе, что случилось."</b> - И не обращай внимания на мой позор.
		}
		<b>"Могу себе представить, как это начиналось"</b>, - отвечает он.

 * { not drugged  } [Скрывать] -> claim_hooper_took_component
 * { drugged  } [Скрывать]
	 	<i>Теперь мой план - это обвинить Хупера, но я не могу рассказать эту историю. Что бы они ни подсыпали мне в чай, оно управляет моим языком. { forceful >1:Я борюсь с этим изо всех сил, но это бесполезно.|Я отчаянно хочу рассказать ему все. Я плачу от стыда.} 
	 	
		 ~ lower(forceful)
-  -> i_met_a_young_man

 	


=== i_met_a_young_man
	//  Объясните Историю
	*	[Говорить]
		<b>"Там был молодой человек. Я встретил его в городе несколько месяцев назад. Мы разговорились о жизни. И я использовал свою легенду, но он, казалось, знал, что это неправда. Это навело меня на мысль, что он может быть одним из нас."</b>
	-	Гаррис меня больше не отпускает. 
		<b>"Вы всерьез рассматривал такую возможность?"</b>
	 * [<b>"Да!"</b>]
	 	<b>"Да, я думал об этом. <>
	 * [<b>"Нет!"</b>] 
		<b>"Нет. Конечно, не больше чем на мгновение. Каждый здесь отмечен тем, как мало мы хотели бы сказать об этом."</b>
		<b>"Только вы сказали этому молодому человеку больше, чем должны были, не так ли?"</b>
		Я кивнул. <b>"<>
	* [Солгать ...] 
		<b>"Через некоторое время я окончательно в этом убедился. После того, как мы поговорили. <>
- 	Казалось, он знает обо мне все. Он... он был совершенно очарован моими достижениями."</b>
	По тому, как Гаррис смотрит на меня, я ожидаю, что он ударит меня, но он этого не делает. Он отвечает:, <b>"Я вижу, как это должно быть привлекательно для вас,"</b> - с такой ясной речью, что я, должно быть, ослышался.

	 *  [<b>"Да!"</b>] <b>"Одинокая жизнь в этом месте,"</b> - Я отвечаю. <b>"Одинок - и все же у тебя никогда нет ни минуты для себя."</b>
		<b>"Это часто бывает на нашей службе,"</b> - отвечает Гаррис. 
		* *	[Поспорить ...] <b>"Но я не обычный военный."</b>
			Гаррис качает головой. -<b>"Да, ты не обычный солдат на фронте, который даже не знает - выживёт ли сегодня. Ты - специалист по безопасности, ценный ресурс страны и агент особого назначения, прямо ответсвенный за безопасность, как и за и жизненно-важный военный успех своей страны. Так что уже раскрой свои глаза!"</b> -  
		* * [Согласиться ...] <b>"Возможно. Но я не выбирал эту жизнь."</b> - 
			Гаррис качает головой. <b>"Верно! Но вместо того, чтобы пойти защищать грудью родину, ты "выбрал" безопасную от пуль и бомб среду. Я не понимаю твоих жалоб! Есть много других, тысячи более достойных ребят, у которых не было и такой возможности как у тебя, которые страдают гораздо хуже. Некоторые гниют заживо, или уже лежат в земле, а ты ..."</b> - 
		- - Затем он отмахивается меня с презрением как от надоедливой мухи.  

	 * (nope) { not drugged  }  [<b>"Нет!"</b>] <b>"Мальчик был довольно простоват. Совсем неполноценный. Его хорошее мнение ничего не значило. Гаррис, пойми меня правильно. Я просто охотился за его телом."</b>
			 ~ raise(evasive)
			<i>Гаррис, к его чести, не вздрагивает, но я вижу, что сегодня ночью ему будут сниться кошмары об этом моменте. Я испытываю искушение протянуть руку и взять его за руку, чтобы сделать ему хуже.
	
	 * { drugged  }   		[<b>"Нет!"</b>] 
	 	<b>"Это не так"</b>, отвечаю я. <b>"Но я сомневаюсь, что ты поймешь."</b>
	 	Он просто кивает. 
	 * { not drugged  }   	[Солгать ...] -> nope

-  <b>"Продолжай свою исповедь."</b>
- (paused) 
	 { not nope:
		<i>Это заставляет меня задуматься. Я не думал об этом как о чем-то подобном. Но я думаю, что он прав. Я собираюсь признаться в том, что сделал.
	}
	<b>"Больше мне нечего сказать. Я взял на себя деталь из вычислительного устройства "Бомба". Вы, кажется, уже знаете это. Я должен был. Он собирался разоблачить меня, если я этого не сделаю"</b>.
	//  Итак, шантаж?
	<b>"Этот молодой человек шантажировал вас из-за романа?"</b>

	~ temp harris_thinks_youre_drugged = drugged

	 { drugged:
	 	~ drugged = false
		<i>Пока Гаррис говорит, я внезапно резко осознаю, как будто просыпаюсь от долгого сна. Стол, рифленые стены хижины - все вдруг кажется более осязаемым, чем мгновение назад.
		<i>Все, что они положили в мой напиток, перестаёт действовать. 
	}
	 
	 * (yes) [<b>"Да!"</b>] 
	 	<b>"Да. Я полагаю, он был их агентом. Я должен был это понять, но не сумел. Потом он пригрозил рассказать Вам. Я думал, что вы меня запрёте: я не мог вынести мысли об этом. Я люблю здесь работать. Я никогда раньше нигде не был так счастлив, так успешен. Я не хотел его терять."</b>
		<b>"Так что вы сделали с компонентом? "</b>- настойчиво говорит Гаррис. Он крепко сжимает перчатки в одной руке, возможно, готовый поднять их и ударить, если потребуется.<b>" Вы уже передали его этому человеку? Вы оставили его где-нибудь, чтобы он его нашел?"</b> - 
		* * (still_have)	[Это вещь у меня ...] 	
				<b>"I still have it. Not on me, of course. -> reveal_location_of_component

		* * (dont_have) 	[У меня нет этой вещи ...] 	-> i_dont_have_it
		* * [Солгать ...] 							-> dont_have
		* * [Рассказать правду ...] 				-> still_have

	 * (notright) [<b>"Нет!"</b>] 
	 	<b>"Нет, Гаррис. Молодой человек меня не шантажировал."</b> - Я глубоко вздыхаю. <b>"Это был Хупер."</b>
		{ not hooper_mentioned:
			<b>"Хупер!"</b> - удивленно восклицает Гаррис. {harris_thinks_youre_drugged:Он не сомневается во мне ни на минуту.}
		- else:
			<b>"Эй, Ты!"</b>, - перебивает Гаррис. - <b>"Хватит рассказывать мне байки!"</b>
		}
		 <b>"Это правда, Гаррис. Если я пойду в тюрьму, пусть будет так, но я не буду висеть Виселице Предателей. Хупер был тем, кто рассказал мальчику о нашей работе. Хупер приставил ко мне мальчика. { forceful < 2:Я, конечно, должен был это понять. Но этого не случилось. Было глупо думать, что они могут.} А затем, как только он скомпрометировал меня, он потребовал, чтобы я украл деталь из машины."</b>
		 ~ revealedhooperasculprit  = true
		<b>"Как ты и сделал."</b> - Гаррис наклонился вперед. <b>"И что дальше? Она всё ещё у тебя? Ты где-то её спрятал?"</b>
		* * (didnt_have_long) [<b>"Да!"</b>] 
			<b>"Да. У меня был только момент. -> reveal_location_of_component

		* * (passed_on) [<b>"Нет!"</b>] -> passed_onto_hooper
		* * [Солгать ...] 			-> passed_on
		* * [Уклониться ...] 		
			<b>"Я не помню."</b>
			Он достает пистолет и легко кладет его на стол.
			<b>"Извини, что угрожаю тебе, но Её Величеству нужен твой мозг, и только. У вас есть много других вещей, без которых наша страна могла бы обойтись. А теперь спрошу еще раз. Где ты спрятал компонент?"</b>
			* * * [<b>"Да!"</b>] -> didnt_have_long
			* * * (nope_didnt_hide) [<b>"Нет!"</b>] 
			 		<b>"Тогда хорошо."</b> - Я нервно сглатываю, чтобы это выглядело более искренним. -> passed_onto_hooper
			* * * [Солгать ...] -> nope_didnt_hide 

			* * * [Уклониться ...] -> i_dont_have_it

	 * [Рассказать правду ...] 	-> yes
	 * [Солгать ...] 				-> notright

= i_dont_have_it
	<b>"У меня его больше нет. Я передал его через забор своему контакту сразу после того, как взял, и сделал это до того, как обнаружилось, что эта деталь пропала. Было бы идиотизмом поступить иначе. Боюсь, её давно нет."</b>
	<b>"Ты дурак, Мэннинг!"</b> - Гаррис ругается, быстро поднимаясь на ноги. <b>"Ты полный дурак. Как ты думаешь, будет ли тебе лучше жить при Гитлере? Это такие люди, как ты, убьют нас всех. Мужчины слишком слабы, слишком слабы в своих сердцах, чтобы встать и взять на себя мужскую ответственность за мир. Вам будет приятнее оставаться ребенком на всю жизнь и играть в свои детские игрушки."</b>
	 * [Сказать в ответ ...]
	 	<b>"Право, командир"</b>, - отвечаю я. <b>"Похоже, вы хотите меня отшлепать."</b>
		<b>"Ради всех святых!,"</b> - Гаррис кричит с сильным отвращением, затем улетает из комнаты.

	 * [Промолчать ...] 
	 	<i>Я молчу. Это правда, и я не могу этого отрицать, что есть мир, сложный мир боли и страданий. И я не могу отрицать, что не задумывался об этом ранее, чем должен был. Какая польза от размышлений о проблеме, которую невозможно решить? Именно наша способность избегать таких бесконечных спиралей делает нас людьми, а не машиной.
		<b>"Боже, смилуйся над этой душой!"</b> - наконец произносит Гаррис, вставая на ноги и направляясь к двери. - <b>"Боюсь, что никто другой не сделает этого."</b> - 

	- -> left_alone

= passed_onto_hooper
	~ hooper_mentioned = true
	<b>"Нет, я передал это Хуперу."</b>
	<b>"Понимаю. И что он с этим сделал?"</b>
	 * [Уклониться ...] 
	 	<b>"Я не знаю."</b>
		<b>"Подумай ещё раз, ты можешь ещё спасти себя. Помни, что предателей ждет петля палача."</b>
		* * 	[Выдвинуть теорию ...] 
				<b>"Ну тогда,"</b> - нервно отвечаю я. <b>"Что бы он сделал? Либо избавьтесь от него сразу - либо, если это было невозможно, что, вероятно было именно так, поскольку ему пришлось бы договариваться со своими контактами - так что, скорее всего, он спрятал его где-нибудь и подождал, и пока веревка была бы на моей шее, он мог быть уверен в своей безопасности."</b>
 				-> claim_hooper_took_component.harris_being_convinced

		* * [Пожать плечами ...] -> claim_hooper_took_component.its_your_problem

	 * [Рассказать правду ...] 
	 	<b>"Я не думаю, что Хупер мог спланировать это заранее. Так что ему нужно будет связаться с кем-то, с кем он работает, а это потребует времени. Поэтому я думаю, что он спрятал бы его где-нибудь и ждал, чтобы убедиться, что я возьму на себя вину. Таким образом, если что—то пойдет не так, он сможет устроить так, чтобы эта часть была удобно найдена."</b>
 		-> claim_hooper_took_component.harris_being_convinced

	 * [Солгать ...]
		<b>"Я уверен, что видел его сегодня вечером, когда он разговаривал с кем-то у забора на лесной стороне участка. Он, наверное, уже передал его кому-то. Вам придется спросить его самого."</b>

		 -> claim_hooper_took_component.harrumphs


/*--------------------------------------------------------------------------------
	Попытка подставить Хупера
--------------------------------------------------------------------------------*/


=== claim_hooper_took_component
// Виноват Хупер
	<b>"Я видел, как его взял Хупер."</b>
	 ~ hooper_mentioned = true
	 { losttemper  :
		<b>"Это был ты?"</b> - 
		<i>Худшая часть его гнева проходит; теперь он впадает в какое-то презрительное отчаяние. Я представляю себе, как он скоро закончит нашу беседу, выйдет из хижины, запрет дверь и бросит ключ в колодец во дворе. 
		<i>А почему бы и нет? С моим запятнанным именем они не позволят мне вернуться к работе над бомбой — если в моем имени есть хоть малейший запах предательства, мне повезет, если меня не запрут до конца войны.
	- else:
		 <b>"Понимаю."</b> - 
		 <i>Он начинает терять терпение. Я несколько раз видел, как Гаррис сердится на лакеев и секретарей. Но только не с нами. С "мозгами" он всегда был осторожен, обращался с нами как с детьми. 
		 <i>И теперь я вижу, что он, как отец, хочет ударить нас, когда мы не повинуемся ему.
	}
	<b>"Просто доберись до правды, парень. Каждая минута важна."</b>
	 * { admitblackmail } [Настоять на своём ...]
	 		<b>"Я знаю, о чем ты думаешь. Если я преступил один раз, то должен быть виновен во всем остальном... Но это не так. Мы были близки к пониманию последней шифровке. Мы получали корреляции в данных. Затем Хупер на мгновение исчез, и в следующую минуту машина была выключена."</b>
	 		
	 * [Рассказать правду ...] 
	 		<b>"Очень хорошо. Я вижу, что нет никакого смысла скрывать это. Ты все равно все знаешь."</b>
			Гаррис кивает и ждет, что я продолжу.
			 -> i_met_a_young_man

	 * { not admitblackmail } [Настоять на своём ...]
	 			<b>"Это чистая правда."</b>
	 
	<b>"Я стал каким—то образом привычным лжецом,"</b> - слова легко слетают с моего языка. Может быть, я и есть предатель, думаю я теперь, когда притворяюсь так же легко, как и он.
		<b>"Продолжай"</b>, - говорит Гаррис, не давая мне понять, верит ли он в мою историю.
		 *	[Утвеждать ...] <b>"Я видел, как он взял его,"</b> - продолжаю я. - <b>"Коллинз сидел на улице и курил. Петерсон сидел за столом. Но я был в передней части машины. Я видел, как Хупер вышел из-за угла. Он наклонился вниз и вытащил что-то. Я даже бросил ему вызов. Я спросил: <b>"Что это? Кто-то забил гвоздь туда, куда не следовало?"</b> - Он не ответил.
		 	Гаррис долго смотрит на меня.
		 	
		 *	[Подразумевать ...] <b>"В тот момент, когда машина остановилась, Петерсон сидел на скамейке, а Коллинз курил на улице. Я проверял переключатели. Хупер был единственным, кто был сзади <b>"бомбы."</b> - Никто другой не смог бы этого сделать."</b>
				<b>"Это не совсем то же самое, что видеть, как он это делает"</b>, - замечает Гаррис.
				 * * 	[Рассуждать логически ...]
				 		<b>"Когда вы устраните невозможное..."</b> - начинаю я, но Гаррис обрывает меня.
		 			
				 * * 	[Говорить убедительно ...] 
				 		<b>"Вы должны мне поверить."</b>
				 		<b>"Мы не обязаны никому верить,"</b> - отвечает Гаррис. <b>"Я буду рад только правде, а твоя история не имеет с ней ничего общего. Мы знаем, что вы оставляете себя открытым для давления. Мы уже некоторое время наблюдаем за вашей деятельностью. Но мы думали, что вы подвергаете опасности репутацию этого места, а не саму страну. Возможно, я слишком доверяю твоей интеллектуальной гордости."</b>
						Он замолкает на мгновение, что-то обдумывая. Затем он продолжает:
						<b>"Это мог быть и Хупер. Это мог быть и ты. -> we_wont_guess

				 * * 	[Говорить Уверенно ...] 
					<b>"Спроси остальных,"</b> - отвечаю я, откидываясь назад. <b>"Они вам скажут. Если они этого еще не сделали, то только потому, что защищают Хупера. Надеясь, что он придет в себя и перестанет быть идиотом. И если вы запрете его в ледяной хижине, как вы сделали это со мной, я уверен, что он это сделает."</b>
						<b>"Нам придётся так поступить!"</b>, - просто отвечает Гаррис. 
						Это все, что я могу сделать, чтобы не разинуть рот.
						-> hoopers_hut_3

	- <b>"У нас остается две возможности. Ты или Хупер."</b> - Командир делает паузу, чтобы пригладить усы. <>
	- (hoopers_hut_3) <b>"Хупер в хижине №3 с капитаном, у него похожий разговор."</b>
	 	
		 *	<b>"А что на счёт других?["] Нам что запереть каждого по отдельности? Достаточно ли здесь старших офицеров, чтобы обойти всех?"</b>
			<b>"Коллинз был снаружи, когда это случилось, а Петерсон не может обойти машину в своем кресле, - отвечает Гаррис. - Значит, остаетесь вы с Хупером.
		 *	<b>"Тогда ты знаешь, что я прав.["] Ты все это время знал. Зачем ты мне угрожал?"</b>
			<b>"Мы знаем только то, что у нас есть предатель, держащий судьбу страны в своих руках. 
	- (we_wont_guess) <> Мы здесь, в Блетчли, не занимаемся гаданием. Мы - это военная разведка. Мы получаем ответы."</b> - Гаррис показывает пальцем. <b>"И если этот компонент покинул эти земли, то каждая минута имеет решающее значение."</b>
	 * [Сотрудничать ...] 
			<b>"С удовольствием помогу,"</b> - отвечаю я, наклоняясь вперед. - Я уверен, что смогу что-нибудь сделать."</b>
			<b>"Например, что именно?"</b>
			* *		 <b>"Дайте встретиться с Хупером."</b>
					 -> putmein
			* *		 <b>"Скажи Хуперу, что я признался.["] Еще лучше. Пусть он увидит, как ты уводишь меня в наручниках. Тогда отпусти его и посмотри, что он сделает. Десять к одному, что он пойдет прямо туда, где спрятал этот компонент, и его игра будет закончена."</b>
					Гаррис медленно кивает, обдумывая эту мысль. Это даже неплохой план — за исключением того, конечно, что Хупер не спрятал компонент и никуда их не приведет. Но эту проблему я, возможно, смогу решить, как только выберусь отсюда; и как только они будут слишком заняты, преследуя шаги Хупера от хижины к хижине.
					<b>"Интересно,"</b> - задумчиво произносит командир. <b>"Но я не уверен, что он настолько глуп. И если он уже передал эту часть, то все это будет только пустой тратой времени."</b>
					* * * 	<b>"Поверь мне. ["] Если я знаю этого человека, а я знаю, он захочет держать свои возможности открытыми как можно дольше. Если компонент исчез, то он в нем по самую шею. Ему понадобится по меньшей мере неделя, чтобы убедиться, что он избежал подозрений. Тогда он передаст это дальше."</b>
							<b>"И если мы будем продолжать давить на него, вы думаете, что компонент в конце концов просто появится?"</b>
							* * * * <b>"Да.["] Вероятно, под моей койкой."</b>
									Гаррис криво улыбается. <b>"Тогда мы будем знать, что это подделка. Мы уже заглядывали туда. 
							* * * * <b>"Или быть выброшенным в реку."</b> - 
									<b>"Хмм..."</b> - Гаррис задумчиво жует ус. <b>"Ну, это поставит нас в затруднительное положение, поскольку мы никогда не узнаем наверняка. Мы должны были быть готовы изменить весь наш подход на тот случай, если эта часть достанется к немцам. 
							- - - -  <> Я не против сказать вам, что это катастрофа, все это дело. Что мне нужно, так это найти этот маленький механический трюк. Мне все равно где. В вашей коробке для завтрака или под подушкой Хупера. Просто где-то, и в пределах этого места."</b>
							* * * * <b>"Тогда пусть думает, что сорвался с крючка.["] Сделай меня звездой шоу. И тогда ты получишь своего человека."</b>
									<i>Таким-то образом</i>, думаю я. Но это та часть, над которой я должен работать.
									 -> harris_takes_you_to_hooper

							* * * * <b>"Тогда вам лучше начать поиски[."</b>],"</b> - отвечаю я, устав от его жалоб. Война есть война, вы должны ожидать врага. -> its_your_problem

					* * * 	<b>"Ты совершенно прав. Дай мне поговорить с ним[."</b>], затем. Как коллега. Может быть, мне удастся вытянуть из него что-нибудь полезное."</b>
	 						-> putmein

					* * * 	<b>"Ты совершенно прав."</b>-> shake_head

	 * [Отстраниться ...] -> its_your_problem


= harris_being_convinced
	<b>"Логично,"</b> - осторожно соглашается Гаррис. { evasive > 1:Я вижу, что он все еще не вполне убежден моим рассказом, а может быть, и нет — я едва ли был полностью откровенен с ним.|Я вижу, что он все еще не уверен, можно ли мне доверять.} <b>"А это значит, что вопрос в том, что мы можем сделать, чтобы сдать его?"</b>
	 * [Предложить помочь ...] 
	 	<b>"Может быть, я смогу помочь с этим."</b>
		<b>"Ах, да? И как именно?"</b>
		* *		<b>"Я поговорю с ним."</b> - 
				<b>"Что?"</b>
				<b>"Организуйте мне встречу с Хупером. Может быть, мне удастся вытянуть из него что-нибудь полезное."</b>
			 	-> putmein
		* *		<b>"Мы его одурачим. ["] Он ждет, чтобы убедиться, что меня вздернули за это, так что давайте дадим ему то, что он хочет. Если он увидит, что меня увели, заковали в кандалы — он сразу же отправится к этому компоненту и начнет избавляться от него."</b>
	 			-> harris_takes_you_to_hooper

	 * [Не предлагать свою помощь ...]
	 	Я откидываюсь назад.  -> its_your_problem

= putmein
	Гаррис качает головой. 
	<b>"Он презирает тебя. Я не понимаю, почему он сдался тебе?"</b>
	 * [Настаивать ...] <b>"Дайте мне шанс. Только я и он."</b> - 
	 	-> go_in_alone
	 * [Бездействовать ...] <b>"Хорошо."</b> - 
	 	-> shake_head


= shake_head
	// Не могу помочь
	<> Я качаю головой. <b>"Вы были совершенно правы. Не вижу, чем я могу вам помочь. Так что есть только один вывод."</b>
	<b>"Ах, да? И что же это?"</b>
	 -> its_your_problem


= its_your_problem
// Не поможет
	<b>"Это ваша проблема. Ваше нарушение безопасности. Так много для вашего тщательного процесса проверки."</b> - 
	Я откидываюсь на спинку стула и складываю руки так, чтобы не было видно, как они дрожат. 
	<b>"Тебе лучше заняться ее решением, вместо того чтобы тратить свое время здесь со мной."</b>
 	-> harrumphs

= harrumphs
	Гаррис хмыкает. Он все обдумывает.
 	* { putmein } [Подождать ...] 
 		<b>"Ладно,"</b> - хрипло заявляет он. <b>"Мы попробуем. Но если это не сработает, я могу просто поставить вас обоих перед расстрельной командой и покончить с этими играми. Во время войны случаются вещи и похуже."</b>
		<b>"Только я"</b>,- добавляю я.
		 -> go_in_alone

 	* { not putmein } [Подождать ...] 
	 	<b>"Нет,"</b> - наконец заявляет Гаррис. <b>"Я думаю, что ты лжешь насчет Хупера. Я думаю, что вы умный, хитрый молодой человек — вот почему мы наняли вас — и вы ищете единственный разумный выход из этой ситуации. Но я его не возьму. Мы знаем, что вы были в комнате с машиной, мы знаем, что у вас извращенные убеждения, мы знаем, что вы скомпрометировали себя. Здесь больше нечего сказать. Или вы скажете мне, что вы сделали с этим компонентом, или мы повесим вас и будем искать так же усердно. Это твой выбор."</b>
	 -> harris_threatens_lynching


= go_in_alone
	<b>"Только ты?"</b>
	<b>"Да."</b>
	Гаррис задумывается. Я наблюдаю за его глазами, бегающими взад и вперед по моим, словно ищет в них подвох.
	* [Спросить терпеливо ...] <b>"И так?"</b>
	* [Спросить нагло ...] <b>"Да ради бога, что вы теряете?"</b> - 
	 	~ raise(forceful)
	-	<b>"Мы будем за дверью,"</b> - серьезно отвечает Гаррис. <b>"Первый признак какого-нибудь странного дела, и вы оба окажетесь на полу через несколько минут. Вы меня понял? Моей стране нужны только твои мозги, но она не слишком беспокоится о твоих ногах. Помни это!"</b>
		Потом он встает, открывает дверь и выводит меня через двор. Приближается вечер, и в воздухе повеяло прохладой. Мой мозг лихорадочно работает. У меня есть только одна возможность — момент, когда я вселю в Хупера страх и заставлю его совершить какую-нибудь глупость, которая подвергнет его опасности. Но как этого добиться?
		<b>"Готов?"</b> - настаивает Гаррис.
 	* (yes) [<b>"Да!"</b>]
 			<b>"Абсолютно."</b>
 	* 	[<b>"Нет!"</b>]
 			<b>"Нет."</b>
			<b>"Но разве у меня есть выбор?!."</b> - 
 	* 	[Соврать ...] -> yes

	- -> inside_hoopers_hut


/*--------------------------------------------------------------------------------
	Быстрый визит к Хуперу
--------------------------------------------------------------------------------*/

=== harris_takes_you_to_hooper
	// В хижине с Хупером
	Гаррис встаёт. <b>"Хорошо,"</b> - говорит он. <b>"Я бы предпочел довериться умному человеку, но мы попробуем."</b> - 
	Затем он улыбается во все зубы, как волк. 
	 { claim_hooper_took_component.hoopers_hut_3:
		<b>"Тем более что это план, который предполагает держать тебя в наручниках. Я не вижу, что мне терять."</b>
	` else:
		 <b>"Хупер в хижине № 3, его допрашивает капитан. Посмотрим, удастся ли нам как-нибудь привлечь его внимание."</b>
	}
	// Веду тебя мимо Хупера
	Он стучит в дверь, вызывая охранника, и дает ему краткие инструкции. Через минуту он возвращается с парой холодных железных наручников. 
	<b>"Поднимите их,"</b> - приказывает Гаррис. Металл смыкается вокруг моих запястий, как капкан. Я встаю и охотно следую за Гаррисом через дверь.
	Но что бы я ни делал со своим телом, мой разум строит козни. <i>Так или иначе,</i> я думаю, <i>что я должен уйти от этих людей достаточно далеко, чтобы получить этот компонент позади Хижины 2 и поместить его туда, куда пойдет Хупер. Или, в противном случае, каким-то образом заставить Хупера пойти туда самому...</i>
	Гаррис ведет меня к хижине № 3 и жестом приказывает охраннику отойти в сторону. Подталкивая меня вперед, он открывает дверь услужливо и широко. 
	// Хижина 3
	<b>"Капитан. Мэннинг заговорил. Не могли бы вы выйти на минутку?"</b>
	 * [Играть свою роль, опустив голову ...]
	 	С того места, где он сидит, я знаю, что Хупер может видеть меня, поэтому я держу голову опущенной и что бы выглядеть виновным за свое <b>"предательство<b>" перед страной. Этот ублюдок, наверное, улыбается.
 		

	 * [Заглянуть внутрь хижины]
		Я заглядываю в дверь и ловлю выражение лица Хупера. Я почти ожидал того, что он улыбнется, но это не так, он выглядит потрясенным и почти убытый страхом. <b>"Йен,"</b> - бормочет он. <b>"Ты не мог..."</b>
		 
	 * 	(shouted) [Позвать Хупера ...] 
	 	У меня есть только один миг, чтобы крикнуть что-то Хуперу, прежде чем дверь закроется.
		<b>"Я достану тебе Хупера, вот увидишь!"</b> Я плачу. Затем:
		 
		 	* *	<b>"Ферзь-ладье два, шах и мат!"</b> [] Кричу я, а потом злобно хохочу, как будто проклинаю его прямо в ад.
			 	~ hooperClueType = CHESS
			- (only_catch) Я только на мгновение улавливаю реакцию Хупера - его бровь приподнимается в удивлении и тревоге. Хорошо. Если он думает, что это угроза, то он просто может быть достаточно беспечным, чтобы пойти искать ответ, что это может означать.
				 
		 	* *	<b>"Не спрашивай, по ком звонит колокол!"</b>
			Он смотрит на меня, как на сумасшедшего, и, возможно, на долю секунды я вижу, как он вздрагивает.
			 

		 	* * <b>"Два слова: грязно, без одного пропавшего!"</b>[] Я плачу, смеясь. Это не самая лучшая подсказка, едва ли достойная того времени, но она должна быть сделана.
		 		~ hooperClueType = CROSSWORD
 			-> only_catch

- 	Капитан выходит на улицу, дергая за ручку двери. <b>"Что это такое?"</b> - спрашивает он. <b>"Признание? Вот так просто?"</b>
	<b>"Нет,"</b> - тихо признается командир. <b>"Боюсь, что нет. Скорее план. Идея в том, чтобы отпустить Хупера и посмотреть, что он сделает. Если он считает, что Мэннинг здесь в кандалах, он попытается вынести компонент с территории базы."</b>
	<b>"Если она у него есть?!"</b>
	<b>"Действительно."</b>
	Капитан пристально смотрит на меня, словно я какое-то любопытное насекомое.
	<b>"Иногда мне кажется, что вы грёбанные волшебники,"</b> - замечает он. <b>"Иногда вы больше похожи на колдунов. Очень сильных и неконтроллируемых."</b> - 
	С этими словами он открывает дверь в хижину и возвращается внутрь. Командир использует этот момент, чтобы грубо толкнуть меня вперед.
	 { shouted  :
		<b>"И из-за чего все эти крики?"</b> - он шипит мне в ухо, когда мы идем к казармам. <b>"Ты пытаешься что-то сделать? Или просто выставить меня некомпетентным?"</b>
	- else:
		<b>"Лучше бы этот твой план провалился,"</b> - шепчет он мне на ухо. <b>"Иначе капитан поставит людей следить за мной, чтобы узнать, куда я хожу по субботам."</b>
	}
	* 	[Заверить коммандира ...] 
		{ not shouted :
			<b>"Так и будет. Хупер испугался,"</b> - отвечаю я, надеясь, что мой голос звучит увереннее, чем я себя чувствую.
		- else:
			<b>"Просто добавляю драматизма,"</b> - уверенно отвечаю я. <b>"Я уверен, что ты можешь это понять."</b>
		}
		<b>"Я думаю, что сегодня у нас уже было достаточно драмы,"</b> - отвечает Гаррис. <b>"Будем надеяться на "точное попадания будет достаточно"."</b>
		
	* 	[Отговорить] 
		{ not shouted:
			<b>"Капитан решил, что это хороший план. Скорее всего, вы получите повышение."</b>
		- else:
			<b>"Я ничего не пытаюсь сделать, кроме как спасти свою шею."</b>
		}
		<b>"Будем надеяться, что все получится,"</b> - мрачно соглашается Гаррис.
		
	* 	[Уклониться ...] 
		<b>"Если они отпустят Хупера, мы все еще будем в опасности. Лучше отведи нас внутрь, и тогда мы сможем поговорить, если понадобится."</b>
		<b>"На сегодня с меня довольно твоего голоса,"</b> - мрачно отвечает Гаррис. <>
		
	* 	[Промолчать ...]
		<i>Я позволил ему продолжить свою тираду. <> 
- 	<i>Он толкает меня вверх по ступенькам казармы, крепко держа, как будто у меня есть хоть какой-то шанс ускользнуть от него, опытного военного. Это все, что я могу сделать, чтобы не упасть в комнату.
 	-> slam_door_shut_and_gone




=== inside_hoopers_hut
	- 	Гаррис открывает дверь и толкает меня внутрь. <b>"Капитан,"</b> - зовет он. <b>"Можно вас на минутку?"</b>
		Капитан с озадаченным видом выходит из машины. Дверь закрыта. Хупер смотрит на меня с открытым ртом, собираясь что—то сказать. У меня, вероятно, есть меньше минуты, прежде чем капитан ворвется обратно и объявит этот план провалом.
	 *	 [Угрожать ...]
	 		<b>"Послушай меня, Хупер. Мы были единственными мужчинами в этой хижине сегодня, так что мы знаем, что произошло. Но я хочу, чтобы ты это знал. Я поместил этот компонент в бриз—блок в фундаменте хижины 2, завернутый в одну из ваших рубашек. В конце концов они его найдут, и это нарушит равновесие. И вы ничего не можете сделать, чтобы предотвратить это."</b>
	 		~ hooperClueType = STRAIGHT
	 		
		Его глаза выпучены от ужаса. <b>"Что я сделал вам? Что я вообще сделал?"</b>
		 * * 	[Рассказать правду ...] 
		 		<b>"Ты обращался со мной как с паразитом. Как что-то отвратительное."</b>
				<b>"Ты - нечто отвратительное."</b>
				<b>"Я не был таким. Ни тогда, когда я приехал сюда. И не буду, когда ты уйдешь."</b>
				
		 * * 	[Солгать ...] 
		 		<b>"Ничего,"</b> - отвечаю я. <b>"Ты просто еще один человек в комнате. Один из нас должен взять вину на себя."</b>
 				
		 * * 	[Уклониться ...] 
		 		<b>"Это не имеет значения. Просто помни, что я сказал. Я победил тебя, Хупер. Помни это."</b>
		 - - 	Я встаю на ноги и открываю дверь хижины. Капитан врывается внутрь, и меня быстро вышвыривают. 		-> hustled_out


	 * [Заключить сделку ...] 
		 <b>"Хупер, я заключу с тобой сделку. Мы оба знаем, что произошло в той хижине сегодня днем. Я знаю, потому что я это сделал, и ты знаешь потому, что ты этого не делал. Но раз это сделано, то я разбогатев, поделюсь этим с тобой. Твоё имя в открытии бомбы. И это не повредит военным усилиям — вы знаете так же хорошо, как и я, что компонент сам по себе ничего не стоит, это проводка бомбы, использование, вот что ценно. Так как насчет этого?"</b>
		Хупер в ужасе оглядывается на меня. <b>"Ты просишь меня совершить измену?"</b>
		 * * 	[<b>"Да!"</b>]
		 		<b>"Да, пожалуй. Но также и для того, чтобы ваше имя вошло в анналы математики. -> back_of_hut_2
		 * * 	[<b>"Нет!"</b>] 
			 	<b>"Нет. Это не измена. Это ремесло, простое и понятное."</b>
	 			
		 * * 	(lie) [Солгать ...] 
		 		<b>"Я предлагаю тебе спасти свою шкуру. Я завернул этот компонент в одну из твоих рубашек, Хупер. Они обыщут это место сверху донизу. В конце концов, они найдут его, и когда они это сделают, это то, что будет весомой уликой против вас. Так что послушайся моего совета. Хижина 2."</b>
				 ~ hooperClueType = STRAIGHT

		 * * 	[Уклониться ...] -> lie
		- - 	 -> no_chance

	 * [Умолять Хупера ...] 
		<b>"Пожалуйста, Хупер. Ты не понимаешь. У них есть информация обо мне. Мне не нужно рассказывать тебе, что я сделал, ты же знаешь. Пойми. А компонент — это ничто. Это не секрет бомбы. Это всего лишь часть. Немцы думают, что это оружие — ракетный компонент. Пусть они его получат. Пожалуйста, чувак. Просто помоги мне."</b>
		<b>"Помочь тебе?"</b> - Хупер вытаращил глаза. <b>"Помочь тебе? Ты предатель. Змея в траве. А ты <i>странный</i>."</b>
		 * * 	[Отрицать предательство ...] 
		 		<b>"Я не предатель. Ты <i>знаешь</i>, что это не так. Сколько я сделал здесь в войне против немцев? Я отдал все свои силы. И ты знаешь так же хорошо, как и я, что если Рейх вторгнется сюда, я буду мертв. Пожалуйста, Хупер. Я не собираюсь сдаваться так просто."</b>
 				
		 * * 	[Согласиться с предательством ...]
		 		<b>"Я такой, какой есть,"</b> - отвечаю я. <b>"Я такой, каким меня сделали. Но они повесят меня, если ты не поможешь, Хупер. Не дай им меня повесить."</b>
 				
		 * * 	[Уклониться от ответа ...] 
		 		<b>"Сейчас это не важно. Важно то, что ты сделаешь сегодня вечером."</b>

		 - 		<b>"Если предположить, что я хотел помочь тебе,"</b> - осторожно отвечает он. <b>"Чего я не хочу. Что бы я сделал?"</b>
				<b>"Ничего. Почти ничего. 
				-> back_of_hut_2

= back_of_hut_2
	<> Все, что вам нужно сделать, это пойти в заднюю часть хижины 2. Там есть Бриз—блок с полостью. Вот куда я его положил. Меня запрут на ночь. Но ты можешь забрать его и передать моему связному. Он будет у южного забора около двух часов ночи."</b>
	~ hooperClueType = STRAIGHT
	 -> no_chance

= no_chance
	<b>"Если ты думаешь, что я это сделаю, то ты сумасшедший,"</b> - отвечает Хупер. 
	В этот момент дверь распахивается, и капитан врывается внутрь.
	 -> hustled_out

= hustled_out
	// В Казарму
	Гаррис тащит меня в казарму. <b>"Надеюсь, на этом все закончится,"</b> - бормочет он.
	<b>"Просто не забудьте выпустить его,"</b> - я отвечаю. <b>"А потом посмотрим, куда он пойдет."</b>
	 -> slam_door_shut_and_gone



/*--------------------------------------------------------------------------------
	Оставшись на ночь в одиночестве
--------------------------------------------------------------------------------*/


=== slam_door_shut_and_gone
	Затем они захлопывают дверь, и она запирается.
	{ hooperClueType == NONE :
		<> И как же я должен что-то делать отсюда?
		* [Попробуйте открыть дверь ...] -> try_the_door
		* [Попробуйте открыть окно ...] -> try_the_windows

	- else:
		Я могу только надеяться, что Хупер клюнет. Если он думает, что я достаточно ожесточен, чтобы подставить его, и достаточно высокомерен, чтобы насмехаться над ним с помощью {hooperClueType > STRAIGHT:подсказки}, где спрятаны проклятые улики... 
		Если он достаточно ненавидит меня и достаточно параноик, тогда он может {hooperClueType > STRAIGHT:разгадать мою маленькую загадку и} пойти искать вокруг Хижины 2. 
	}

	 * [Ждать до утра ...] -> night_falls


= try_the_door
	Я пытаюсь открыть дверь. Она, конечно, заперта. 
	 -> from_outside_heard

= from_outside_heard
	Снаружи я слышу чей-то голос. Это Хупер с кем-то разговаривает.
	- (opts)
	* (listened) [Подсушать через замочную скважину ...] 
			Я приложил ухо к замочной скважине, но там ничего не было. Вероятно, снаружи все еще стоит охрана, но они молчат.
			-> opts

	* { not try_the_windows } [Попробуйте открыть окно ...] -> try_the_windows
	* { not try_the_door } {listened} [Попробуйте открыть дверь ...] -> try_the_door
	* { try_the_windows } [Попробовать разбить окно ...] -> try_to_smash_the_window
	* { try_the_door && try_the_windows } [Ждать до утра ...] 
	 		Это бесполезно. Мне ничего не остается, как надеяться. Я сажусь на край койки и жду.
 			-> night_falls

= try_the_windows
	Я подхожу к окну и пытаюсь его открыть. Безрезультатно, но в попытках я замечаю, что это окно выходит только на тонкий маленький ручеек, который бежит вниз по задней части комплекса. А это значит, что если я разобью его, то смогу уйти так, что никто не увидит.	 
	 -> from_outside_heard


= try_to_smash_the_window
	Окно - это мой единственный выход отсюда. Мне просто нужен способ разбить его.
	 * [Ударить по окну ...] 
	 	Я думаю, что мой кулак будет достаточно хорош. Но, скорее всего, тогда я пораню себя, если буду пытаться разбить окно кулаком. <>

	 * (use_bucket) [Найди что-нибудь ...] 
			 ~ smashingWindowItem = BUCKET
			Я осмотрел маленькую комнату. В углу стоит ведро на всякий случай — думаю, оно мне пригодится. Я поднимаю его, но это не очень легко поднять. <>
	 * [Используй то, что у тебя есть ...] 
		 	Я похлопываю себя по карманам, но все, что у меня есть, - это <b>"листок с перехваченными сообщениями"</b>, который никуда не годится.
			* * [Что-то на тебе надето? ...] 
					Ах, ну конечно! Я снимаю одну туфлю и поднимаю ее за носок. Каблук будет достаточно приличным молотком, если я буду бить им достаточно сильно.
					 ~ smashingWindowItem = SHOE
					Но  тогда я порежу свою руку, делая это. <>
			* * [Посмотрите вокруг] -> use_bucket
	- 	И шум будет ужасный. Должен же быть способ сделать это проще. Теперь я должен быть вором. Что бы сделал грабитель?
	 	* [Работать медленно ...] 
	 		Работать аккуратно? Трудно работать осторожно, когда все, что у тебя есть, - это { smashingWindowItem == BUCKET :ведро. Это скорее похоже на кувалду для пресловутой гайки|{ smashingWindowItem == SHOE :туфля|ничего, кроме грубой силы}}. 
			 * * [Просто сделай это ...] -> time_to_move_now
			 * * [Оглянитесь вокруг в поисках чего-нибудь ...] 
	 	* [Найди что-нибудь, чтобы помочь ...] 
	- -> find_something_to_smash_window


= time_to_move_now
	Хватит об этом. Нет никакого времени чтобы проирывать. Прямо сейчас они будут следить за Хупером, когда он ляжет спать и заснет; и тогда все будет кончено. В ту минуту, когда он закрывает глаза и засыпает, эта ловушка захлопывается передо мной.
	Поэтому я выбиваю стекло своим { smashingWindowItem == BUCKET :ведром|{ smashingWindowItem == SHOE :туфлёй|кулаком}}, и оно разбивается с ужасающим шумом. Затем я останавливаюсь и жду, не войдет ли кто-нибудь в дверь.
	Ничего.
	 * (pause) [Подождать еще немного ...] 
		 Я останавливаюсь еще на мгновение. Не стоит быть слишком беспечным...
	 * [Очистить рамку от осколков ...]
		Обмотав куртку вокруг руки, я сметаю оставшиеся осколки стекла. Это не большое окно, но я не большой человек. Если бы я был Гаррисом, я был бы сыт по горло, и ... так оно и есть...

	 - Потом дверные замки поворачиваются. Дверь открывается. Затем Джереми — вернее, один из охранников — просунул голову в дверь. <b>"Мне показалось, я слышал..."</b> - 
		Он останавливается. Смотрит на мгновение. { smashingWindowItem ==BUCKET :видит ведро в моей руке.|Видит разбитое окно.} Затем, не раздумывая ни секунды, он пронзительно свистит и врывается в хижину, грубо хватая меня за руки.
		{ pause:
			Я никогда не узнаю, если бы не ждал этого дополнительного момента — может быть, я все еще мог бы уйти. Но как далеко?
		}
		Меня втолкнули в одну из хижин. Спать негде, но они больше не интересуются моим комфортом. Гаррис входит вместе с капитаном.
		<b>"Итак,"</b> - замечает Гаррис. <b>"Похоже, твоя маленькая ловушка захлопнулась. Только это сработало, чтобы показать <i>вам</i> кто вы такой."</b>
		* 	[Рассказать правду ...] 
			 { i_met_a_young_man  :
				<b>"Пожалуйста, Гаррис. Вы не можете понять, какое давление они на меня оказывают. Ты не можешь понять, каково это-быть влюбленным, но ничего не можешь с этим поделать..."</b>
			- else:
				<b>"Гаррис. Они шантажировали меня. Они знали об этом... те или иные неблаговидные поступки. Вы можете понять, не можете вы, Гаррис? Я был в безвыходном положении..."</b>
			}
		* 	[Солгать ...]
			 <b>"Мне пришлось уйти, Гаррис. Я должен был спровоцировать Хупера на что-то, что полностью изобличит его самого. Видите ли, он слишком умен..."</b>
			 
		* 	[Уклониться ...] 
		 	<b>"Это ничего не доказывает,"</b> - упрямо отвечаю я. <b>"У вас все еще нет компонента, а без него я не вижу, что вы можете надеяться доказать."</b>

	 - <b>"Успокойся, парень. Мы знаем все о твоих грязных делах."</b> - Капитан кривит губы. <b>"Разве ты не знаешь, что идет война? Знаешь, куда бы тебя послали, если бы не твой мозг? Не кажется ли вам, что вы обязаны своей стране использовать его немного больше?"</b>
		 
		</>Действительно ли</i>, а я сам-то знаю? <i>Разве я чем-то обязан этой стране, этой стране, которая отвергла меня с того самого дня, как я стал мужчиной?</i>
		 * [<b>"Да!"</b>] 
			 	Мой гнев спадает, как рушащееся уравнение, все аргументы сводят на нет друг друга. Мир, конечно, ничем мне не обязан, а я обязан ему всем.
			 
		 * (alone) [<b>"Нет!"</b>] 
				 <i>Конечно же нет. Я одинок; это то, что они хотели, чтобы я был, из-за того, кого и что я люблю. Так что у меня нет ни нации, ни страны.</i>
				 

		 * [Солгать ...] 	-> alone
		 * [Уклониться ...] 	
		 		<i>Но что такое страна, в конце концов? Страна-это не концепция, не идеал. Каждая страна падёт перед течением времени, ее границы сдвигаются и перемещаются, ее язык исчезает, чтобы быть замененным другим. Ни Рейх, ни Британская империя не будут жить вечно, так что какая польза от моей преданности им обоим? </i>
				<I>Возможно я, таким же образом забочусь о себе. То, что я пытался сделать, оно с треском провалилось.</i>
				 
	- // Скажи нам, где
		<b>"Боюсь, у нас есть только один выход, Мэннинг,"</b> - говорит Гаррис. <b>"Пожалуйста, дружище. Скажите нам, где находится компонент."</b>
		 ~ notraitor = true
		 ~ losttemper = false
		 * [Сказать им ...]
		 	~ revealedhooperasculprit = false
		 	<b>"В порядке."</b> - В конце концов, я побежден. <b>"<>-> reveal_location_of_component

		 * [Промолчать ...] -> my_lips_are_sealed

= find_something_to_smash_window
	Дайте-ка подумать. Вот койка, { not smashingWindowItem == BUCKET :ведро,} и больше ничего. У меня есть пиджак, но в карманах ничего нет — например, носового платка.
	- (opts)
	* 	[Койка] 	
		Койка имеет прочную металлическую раму, одеяло, подушку, ничего больше.
		- - (bunk_opts)
		* * [Металлическая рама ...]
			 	Рама тяжелая и прочная. Я не мог ни поднять его, ни сдвинуть без посторонней помощи. И в любом случае это не принесет мне никакой пользы. Я прекрасно могу дотянуться до окна.
				 -> bunk_opts
		* * [Одеяло ...] 
		 		Одеяло. Идеально. Я поднимаю его с кровати и держу над окном. -> smash_the_window
		* * [Подушка] 
		 		Подушка толстая и пушистая. Я мог бы повесить его на окно, и это, конечно, приглушило бы звук бьющегося стекла, но я также не смог бы разбить ни одно стекло через него. 
				 -> bunk_opts

		* * {bunk_opts > 1} [Что-то еще ...] -> opts

	* [Куртка] 
			Я снимаю куртку и держу ее одной рукой над стеклом. -> smash_the_window
	* { not smashingWindowItem == BUCKET } [Ведро ...] 
	 		Ведро? Едва ли. Ведро могло бы помочь, если бы я захотел потом подмести стекло, но оно не поможет мне разбить стекло тихо.
		 	-> opts


=== smash_the_window
	// Разбивание стекла
		Затем я поднимаю { smashingWindowItem == BUCKET :ведро — это действительно довольно неудобно делать в наручниках — |{ smashingWindowItem == SHOE : мой ботинок за носок, |мой сжатый кулак, }} и делаю сильный взмах, пытаясь представить, что это лицо Гарриса с другой стороны.
	 	~ smashedglass  = true
	 	~ smashingWindowItem = NONE
		*	[Разбить окно сейчас! ...]
	-	Звук удара приглушен. Все еще держа руку прикрытой, я вытряхиваю остатки стекла из рамы. 
	- 	Я готов бежать. Беда только в том, что, когда они заглянут ко мне утром, у них не будет никаких сомнений в том, что произошло. Это ни на йоту не поможет мне избавиться от подозрений.
		* [Подождать до утра ...]
		 		Так что, возможно, мне все-таки стоит переждать. Кто знает? Возможно, позже у меня будет лучшая возможность.
			 	-> night_passes
		* [Совершить побег через разбитое окно ...] 
		 		Двигаясь быстро и бесшумно, я забираюсь на оконную раму и выбираюсь наружу, в морозный ночной воздух. Затем я бегу, скользя по тропинкам между хижинами, держась в тени, по пути к Хижине 2.
	// Выход на улицу ночью
	-
		 * [Иди кратчайшим путем ...] 
			 	Нельзя терять время. Отбросив осторожность, я быстро пробираюсь к Хижине 2 и огибаю ее сзади. Не думаю, что меня видели, но если и видели, то уже слишком поздно. Мои действия достаточно подозрительны что бы меня ликвидировали как предателя родины. У меня нет другого выбора, кроме как идти до конца.
		 * [Возьмите более длинный маршрут ...]
		 		На случай, если за мной следят, я отклоняюсь по периметру комплекса. Это гораздо более длинный путь, и он ведет меня по неизвестной местности, которую трудно преодолеть в темноте - грязной и густой от чертополоха и гнезд.
				~ muddyshoes = true
				И все же я могу быть уверен, что за мной никого нет. Я присел на корточки за задней стеной Хижины 2. <>
	- 	Компонент все еще там, завернутый в полотенце и засунутый в полость в шлакоблока у основания стены хижины.
	 	* [Взять компонент ...] 
	 		В быстром темпе, я легко его вытаскиваю, и кладу его в карман моей куртки.
			~ gotcomponent = true
		
	 	* [Оставить компонент ...] 
	 		Тем не менее, это означает, что никто не нашел его, а значит, он, вероятно, хорошо спрятан. И, если не считать того, что я сейчас сбегаю из комплекса, я могу позволить себе оставить его там еще на некоторое время. Поэтому я оставляю его на месте.
	- Куда теперь?
	 	* [Вернуться в казарму ...] -> return_to_room_after_excursion
	 	* { gotcomponent } [перейти в общежитие Хупера] -> go_to_hoopers_dorm
 		* [Сбежать из комплекса ...] 
			Я был уже здесь достаточно, и мне пора двигаться прочь отсюда. Я могу дойти до вокзала пешком, сесть на почтовый поезд до Шотландии и оказаться где-нибудь еще, прежде чем кто-нибудь заметит, что меня нет.
			 
			Конечно, тогда они будут искать меня всерьез. { not framedhooper :уже точно как предатель.Возможно, не как предатель — они могут подумать, что Хупер причастен к краже, — но определенно как ценный разум, хранящий ценные секреты и слишком легко поддающийся угрозе. Они подумают, что я убегаю из-за своей неосторожности. Справедливости ради, я полагаю, что так оно и есть.}
			* * [Убежать ... ] -> live_on_the_run
			* * [Не двигать ...] 
				 	Это никуда не годится. Это только половина решения. Я не мог быть удовлетворен только таким исходом событий.
					* * * [Назад в казарму ...] -> return_to_room_after_excursion
					* * * { gotcomponent && not go_to_hoopers_dorm } [Вернуться в общежитие Хупера ...] -> go_to_hoopers_dorm


/*--------------------------------------------------------------------------------
	Посетите общежитие Хупера ночью
--------------------------------------------------------------------------------*/


=== go_to_hoopers_dorm
	// Общежитие Хупера
	Я крадусь вокруг хижин к общежитию Хупера. Пора покончить с этой маленькой игрой раз и навсегда. Несколько охранников патрулируют этот район ночью, но не так уж много — в конце концов, очень немногие знают, что это место вообще существует.
	Наши покои расположены вдали от главного дома; где мы спим, имеет меньшее значение, чем где мы работаем. У каждого из нас есть своя хижина, хотя некоторые из них менее постоянны, чем другие. Хупер - палатка военного образца: довольно большой навес, с двумя комнатами внутри и короткой верандой, где он настаивает, чтобы люди оставляли свою обувь. Он весь застегнут на молнию на ночь, и ни луча света не светит из хижины.
	Я на мгновение задерживаюсь. Если Гаррис соблюдает условия нашей сделки, то кто-то будет следить за этим местом. Но я никого не вижу.
	 * (outer_zip) [Открыть наружную молнию ...] 
		 	Я подкрадываюсь к палатке, намереваясь немного приоткрыть молнию на крыльце достаточно, чтобы просунуть компонент внутрь, и без лишнего шума, который может разбудить храпящего Хупера.
			Хупер завязал молнии изнутри, маленький привередливый ублюдок! — но после небольшой работы мне удается сделать отверстие достаточно большое для моей руки.
			* * [Положить компонент в палатку ...] 		
					Я засовываю деталь в палатку, застегиваю молнию и быстро удаляюсь в тень. Проходит несколько минут, прежде чем мое дыхание замедляется, а сердце перестает колотиться, но я не вижу никакого другого движения. Если кто-то и наблюдает за палаткой Хупера, то они спят на своих постах.
					 ~ putcomponentintent = true
					 ~ gotcomponent = false
					 -> return_to_room_after_excursion
			[Нет, попробовать как-нибудь по-другому ...] 			
					Заминка. Слишком прозрачно. Слишком заметно. Если я оставлю его здесь, Хупер никогда не пойдет искать его: он споткнется об него на виду у всех, и люди, наблюдающие за ним, будут удивляться, почему его не было там, когда он ложился спать.
					Нет, я должен попробовать что—то другое-или вообще ничего.
					* * * [Положить компонет сверху палатки ...] -> put_component_on_tent
					* * * [Бросить компонент в высокую траву ...] 
					 	По вдохновению — или от отчаянию, я не уверен — но ко мне пришёл в голову более простой способ. Просто выбросит в траву. Чем ближе к хижине Хупера, тем очевиднее его причастность к краже компонента. -> toss_component_into_bushes
					* * * [Сдаться, так ничего не предприняв ...] 
							Здесь нет ничего из чего можно было бы получить выгоду. Теперь у меня есть этот компонент; может быть, завтра он будет иметь какую-то ценность.
							* * * * [Вернуться в комнату ...] -> return_to_room_after_excursion
							* * * * [Убежать из комплекса ...] -> live_on_the_run

	 * (wide_circuit) [Найти другое отверстие в палатке Хупера ...] 
		 	Сделав широкий круг, я крадусь вокруг палатки. У палатки Хупера есть множество других створок и отверстий, связанных с Гордиевой сложностью. Но ничто не позволяет себе проскальзывать компоненту внутрь.
			* * [Попробуйте расстегнуть молнию ...] -> outer_zip
			* * [Положить компонент сверху палатки ...] -> put_component_on_tent
			* * [Сдаться, так ничего не предприняв ...] 						
				Это никуда не годится. Ничто из того, что я могу сделать, не будет менее очевидным — если вдруг что-то появляется там, где чего-то не было раньше. Люди, наблюдающие за Хупером, поймут, что это обман, и протесты Хупера будут приняты за чистую монету.
				Если я не могу найти способ, чтобы Хупер подобрал компонент, как будто из своего собственного тайника, и был пойман за этим занятием, то у меня нет никакого плана вообще.
				* * * [Вернуться в комнату ...] -> return_to_room_after_excursion
				* * * [Убежать из комплекса ...] -> live_on_the_run
				* * * [Бросить компонент в кусты ...] -> toss_component_into_bushes

	 * [Спрятать компонент где-нибудь неподалёку ...] 
	 		Если я оставлю компонент устройства где—то здесь, он должен быть достаточно близко, где я могу быть уверен что только Хупер найдет его, и достаточно скрытно, что никто другой до этого момента его точно не найдёт.
			* * [Спрятать компонент за палаткой ...] -> wide_circuit
			* * [Положить компонент внутри секции крыльца ...] -> outer_zip
			* * [Положить компонент поверх палатки ...] -> put_component_on_tent


= put_component_on_tent
	Мне пришла отличная идея. Если бы я смогу забросить его на верх палатки, куда-нибудь посередине, где он будет провисать вовнутрь, тогда он был бы невидим для любого проходящего мимо. Но Хупер просто не сможет перебороть своё любопытство: когда он просыпается он увидит прогиб палатки, что очевидно было вызвано тем, что нечто упало сверху пока он спал. Что может быть естественнее, чем встать, выйти и посмотреть, что на него свалилось ночью?
	
	Это работа одного мгновения. Когда-то я был отличным подающим кручёный мяч в сборной команде старшей школы. И на этот раз я, конечно, и бросаю жизненно важный компонент именно туда, куда мне нужно. 
	 ~ framedhooper = true
	 ~ gotcomponent = false
	На секунду я задерживаю дыхание, и как ожидалось, всё тихо и спокойно. Диверсия успешно реализована. -> return_to_room_after_excursion


= toss_component_into_bushes
	Я бросаю деталь в кусты за палаткой Хупера и возвращаюсь в свой барак, желая себе долгого сна, а затем утра, свободного от этого дела.
	 ~ gotcomponent = false
	 ~ throwncomponentaway = true
	 -> return_to_room_after_excursion

/*--------------------------------------------------------------------------------
	Концовка: бегите из лагеря
--------------------------------------------------------------------------------*/


=== live_on_the_run
	Лучше жить в бегах, чем умереть на вертеле. Крадучись по краю комплекса{ gotcomponent :, тяжелый компонент Bombe в моем кармане}, я пробираюсь к главным воротам. Как всегда, там дежурят два охранника, но я проскальзываю мимо их будки, ползая на животе.
	И вот я уже в пути. Шел, а не бежал. Тихий. Свободный.
	// Конец - Убегай
	По крайней мере, на данный момент.
	-> END

/*--------------------------------------------------------------------------------
	Вернитесь в комнату после того, как выскользнете
--------------------------------------------------------------------------------*/


=== return_to_room_after_excursion
	{ gotcomponent :Запчасть устройства Bombe приятно отягащает мой карман в моей куртке|Довольный}, я возвращаюсь коротким путем вверх по дорожкам между хижинами к барачному блоку и разбитому окну. 
	Немного труднее пролезть обратно — окно выше над землей, чем пол внутри, — но после приличного количества прыжков и рывков мне удается поднять локти, а затем одну ногу, и, наконец, я падаю внутрь, совершенно запыхавшись и задыхаясь.
	 * [Отдыхать до утра ...] -> night_passes

/*--------------------------------------------------------------------------------
	Проходит ночь, наступает утро
--------------------------------------------------------------------------------*/


=== night_passes
// В комнате разбито стекло
	Остаток ночи проходит медленно. Я сплю немного, в основном дремлю. Потом меня будит петух во дворе. Дверь открывается, и входит Гаррис. Он бросает взгляд на разбитое окно и озадаченно хмурится.
	{ putcomponentintent: -> put_component_inside_tent }

	- Что там произошло?"</b>
 	* [Исповедоваться в содеянном ...] 
	 	<b>"Я разбил окно,"</b> - отвечаю я. Понимая, нет никакого смысла это отрицать. <b>"Я думал, что смогу сбежать. Но я не мог заставить себя пройти через это. Я не смог убежать."</b>
		Командир смеется. -> glad_youre_here 
	
	* (deny) [Отрицать ...] 
	 	<b>"Я не знаю кто это сделал. Я спал и проснулся, когда кто-то разбил окно. Я выглянул, чтобы посмотреть, кто это, но они уже ушли."</b>
		Гаррис озадаченно смотрит на меня. <b>"Кто-то пришел разбить окно, а потом убежал? Это же абсурд. Это полный абсурд. Признай это, Мэннинг. Ты пытался сбежать и побоялся пойти до конца."</b>
		* * [Признать это ...]
	 		<b>"Хорошо. {forceful>11:Будь ты проклят.} Так всё и было."</b>
	 		-> glad_youre_here

	 	* * { not framedhooper } [Отрицай это ...]
	 		<b>"Если бы я хотел сбежать, то сделал бы это наверняка,"</b> - сурово говорю я ему. 
	 		-> harris_certain_is_you

	 	* * { framedhooper } [Отрицай это ...] 
		 	<b>"Говорю тебе, кто-то другой разбил окно. Мне кажется, кто-то хотел расправиться со прошлой ночью."</b>
			Гаррис качает головой. <b>"Ну, мы можем заняться этим вопросом позже. А сейчас ты, вероятно, хочешь услышать более важные новости. -> found_missing_component

 	* { gotcomponent } [Показать ему компонент ...] -> someone_threw_component

= put_component_inside_tent
	Он оглядывается вокруг, затем глубоко и тоскливо вздыхает .		
	<b>"Дела твои всё хуже и хуже, Мэннинг,"</b> - замечает он. <b>"Ты сам себе злейший враг."</b>
	 * [Согласиться с этим ...] 
	 	<b>"Я и раньше так думал."</b> - { admitblackmail :Конечно, в том, что касается шантажа.}
		<b>"Позвольте мне рассказать тебе, что произошло сегодня утром. <>

	 * [Отрицать ...]
	 	<b>"Сейчас, я думаю, ты возьмешь на себя эту роль, Гаррис,"</b> - холодно отвечаю я.
	 	- - (droll) <b>"очень смешно"</b>, - отвечает он. <b>"Позвольте мне рассказать вам, что произошло сегодня утром. Это снимет улыбку с Вашего лица. <>

	 * [Уклониться ...] 
	 	<b>"Я с нетерпением жду возможности помыться и сменить одежду, что должно сделать меня немного менее злым, чтобы быть рядом."</b>
	 	-> droll

	- 	Наши люди, наблюдавшие за палаткой Хупера, видели, как он проснулся, оделся, выбрался из палатки и наступил на что-то у входа в палатку."</b>
	 	~ piecereturned = true
	 * [Поинтересоваться ...] 
	 	<b>"Ты хочешь сказать, что он даже не скрывал этого? Он положил его в ботинок?"</b>
	 	- - (not_that) <b>"Нет,"</b> - отвечает Гаррис. <b>"На самом деле я не это имел в виду. <>

	 * [Вести себя пренебрежительно ...]
	 	<b>"Значит, он идиот, раз спрятал его в ботинке."</b>
		 -> not_that

	 * [Промолчать ...] 
	 	Я говорю тихо, прислушиваясь, не зная, как все пойдет.
		<b>"На случай, если я не совсем ясно выразился,"</b> - продолжает Гаррис, - <b>"<>

	- Я имею в виду, что ему удалось найти его, случайно, где-то, где его не было прошлой ночью. И в то же время ты сидишь здесь с разбитым окном. Так что, я думаю, вы сыграли свою последнюю партию и проиграли. Совершенно невероятно, что Хупер украл этот компонент, а затем оставил его валяться в дверях своей палатки. Поэтому я пришел сказать тебе, что игра окончена."</b>
		Он кивает и встает. -> left_alone



= someone_threw_component
	<b>"Кто-то бросил это в окно ночью,"</b> - отвечаю я и расстегиваю куртку, чтобы показать компонент от бомбы. <b>"Я не видел, кто это был, было слишком темно. Но я знаю, что это такое."</b>
	Он протягивает руку и забирает деталь. <b>"Ну, будь я проклят,"</b> - бормочет он. <b>"Все в порядке. И у тебя его не было, когда мы тебя сюда положили. Но это не мог быть Хупер — мои люди следили за ним всю ночь. И это не мог быть никто другой."</b> - 
	Он ошеломленно вертит в руках деталь.
	 ~ piecereturned = true
	 * [Предложите что-нибудь ...] 
	 	<b>"Возможно, у Хупера был сообщник. Кто-то еще, кто работает на месте."</b>
		Гаррис рассеянно качает головой. <b>"Это бессмысленно,"</b> - говорит он. <b>"Зачем тратить столько сил на кражу только для того, чтобы вернуть его? И почему именно так?"</b>
		 * * [Предложи вариант ночного события ...] 
		 	<b>"Возможно, сообщник решил, что здесь держат Хупера. Может быть, они видели охранника..."</b>
		 	-> all_too_farfetched
		 * * [Промолчать ...] 
	 * [Промолчать ...] 
	- Я красноречиво пожимаю плечами.
	- -> all_too_farfetched


= glad_youre_here
	<b>"Стыдно,"</b> - замечает он. <b>"Мне следовало оставить окно открытым и приставить к тебе охрану. Было бы интересно посмотреть, куда вы пошли. В любом случае, я рад, что ты все еще здесь, даже если от тебя пахнет собакой."</b>

	* { not framedhooper } [Быть оптимистом ...] 
	 	-> night_falls.morning_not_saved.optimism
	* { not framedhooper } [Или, наоборот быть пессимистом ...] 
		-> night_falls.morning_not_saved.pessimism

	* { framedhooper } [Быть оптимистом ...]  
	 		<b>"Я с нетерпением жду ванны."</b>
			// Хупер был подставлен как предатель
			<b>"Ну, тебе это должно понравиться. <> 

	* { framedhooper } [Или, наоборот быть пессимистом ...]
	 		<b>"Думаю, через пару дней от меня будет еще хуже пахнуть."</b>
			<b>"В этом нет необходимости. <>
	- -> found_missing_component


= found_missing_component
	// Хупер был подставлен как предатель
	Мы нашли недостающий компонент. Вернее, Хупер нашел его для нас. Он выскользнул и поднял его сверху. Чёрт, мы бы никогда не узнали, что оно там было. Конечно, когда мы на него набросились, он заявил, что ничего не знает. Но для меня этого достаточно."</b>
	 * (devil) [Одобрить такой результат ...]
			<b>"Я не могу рассказать вам достаточно, но я рад это слышать. У меня была ужасная ночь."</b>
			 Он бросает взгляд на разбитое окно, но только на мгновение. Я думаю, он искренне не может поверить, что я мог это сделать.
	 * [Не одобрять ...] 
	 		<b>"Тебе не следовало его нанимать. Глупо ожидать, что интеллект ниже среднего справится с давлением нашей работы."</b>
 	- Гаррис закатывает глаза, но на самом деле он почти улыбается. - Тебе лучше идти дальше, { devil :и работать со своими дьяволами|Мистер интеллигент}. Есть сообщение с 24—часовым опозданием, которое нужно решить, и мы гениально коротки. Так что вам лучше быть готовым работать в два раза усерднее."</b>
 		* 	[Поблагодарить Гарриса...] 	
 			<b>"Мне это понравится. Спасибо, что помогли мне все прояснить."</b>
			<b>"Ещё рано для благодарностей. Ещё целая война впереди. А теперь пошевеливайся."</b>
			Я киваю и торопливо выхожу за дверь. Воздух снаружи никогда не был таким свежим и бодрящим. <>

 		* [Поспорить с ним ...] 
 				<b>"Я буду работать так же усердно, как работал до этого."</b>
				<b>"Убирайся,"</b> - рычит Гаррис. <b>"Прежде чем я решу арестовать тебя как соучастника."</b>
				Я делаю, как он говорит. За пределами казармы воздух никогда не пах слаще. 
	- -> head_for_my_dorm_free


=== night_falls ===
// Наступает ночь
	Наступает ночь. Часовой механизм небес продолжает вращаться, в каком бы состоянии я ни находился. Никто не может украсть компоненты, которые заставляют солнце садиться и звезды выходить. Я наблюдаю как медленно вращается космос. Я не могу уснуть.
	{ hooperClueType > NONE  :
		Неужели Хупер заглотил мою наживку?
	}
	* [Посмотреть в окно ...] 
			Я выглядываю в окно, но оно выходит на маленький ручеек в задней части комплекса, и оттуда не видно ни других хижин, ни самого дома. Кто знает, есть ли там люди, обыскивающие основание Хижины номер 2, следуя друг за другом с фонариками...
			 {inside_hoopers_hut.back_of_hut_2:
			 	Может быть, Хупер все-таки там, в темноте, пытается мне помочь?
			 }
	* [Подслушивать у двери ...] 	
			Я прикладываю ухо к замочной скважине, но ничего не слышу. Там все еще выставлена охрана? { hooperClueType > NONE :Возможно, если Хупер сумел изобличить себя, охранники были удалены?|Возможно, компонент был найден, и опасность миновала.}
			Может быть, дверь не заперта, и они оставили меня спать? 
			[Попробовать открыть дверь ...] Я дергаю ручку. Резрезультатно.
			[Не трогать дверт ...] Я не прикасаюсь к двери. Я не хочу, чтобы кто-то снаружи подумал, что я пытаюсь сбежать.
			 
	* 	[Ждать ...] 					
			Я ничего не могу сделать, чтобы ускорить время. 

	- Ночь движется в своем собственном темпе. Полагаю, к утру я узнаю свою судьбу.
 	* { hooperClueType > NONE } [Ждать дальше ...] 
 		// Хупер арестован
		Наступает утро. Меня будит петух, кричащий со двора за домом. Должно быть, я все-таки спал. Я поднимаюсь с койки, слегка дрожа. На внутренней стороне окна есть конденсат. Я, наверное, простудился.
		Без стука, Гаррис приходит внутри. <b>"О! Ты уже встал,"</b> - замечает он, а потом добавляет: <b>"От тебя пахнет, как от животного."</b>
		* * 	[Быть дружелюбным ...] 
				<b>"Полагаю, что так оно и есть."</b> - Я смеюсь, а Гаррис-нет.
				<b>"Эта чертова история становится все хуже и хуже, - говорит он, подходя к окну, чтобы отпереть его и распахнуть. <>
		* * 	[Быть спокойным ...] 
				<b>"И ты тоже,"</b> - язвительно отвечаю я. Гаррис пожимает плечами.
				<b>"Бывало и похуже,"</b> - сухо отвечает он. <b>"Вряд ли я виноват, что ты спишь в одежде."</b>
				Я смотрю на него в ответ. Он подходит к окну, отпирает его и распахивает настежь, наслаждаясь свежим воздухом снаружи.   
		- 		<b>"Хупер признался, вы знаете."</b>
		* * 	[Быть нетерпеливым ...] 
				<b>"Он сознался? Я знал, что так и будет. Червь."</b>
				<b>"Теперь спокойно. Но это еще не конец. <> 
		* * 	[Быть настороженым ...] 
				<b>"Ах, да?"</b>
				<b>"Да. Как бы то ни было. <> 
		- - 	(hooper_didnt_give_himself_up) Проблема всё еще не решена. Компонент так и не был найден. Хупер не привел нас к нему. Наверное, он решил, что у тебя на него что-то есть. Я не знаю."</b>
 				
				Он выглядит совершенно обескураженным всем этим делом. Он не из тех людей, которые хорошо разбираются в вероятностях.
 		* * 	[Быть заинтересованным ...] 
 				<b>"Вы хотите сказать, что он признался по собственной воле? Вы его не поймали?"</b>
				
 		* * 	[Быть бескорыстным ...] 
 				<b>"Ну, я рад, что его совесть наконец-то настигла его,"</b> - отвечаю я пренебрежительно.
		- - 	Капитан вернулся в ту хижину и немедленно во всем сознался. Мы были так удивлены, что не отпустили тебя."</b> - Он морщит нос. <b>"Теперь я очень сожалею об этом. Я предлагаю тебе помыться."</b>
				И с этими словами он указывает на дверь.
 			* * 	[Выйти из комнаты ...] 
 			* * 	[Ждать ...] 
 				Я на мгновение задерживаюсь. Что-то кажется не совсем правильно. В конце концов, Хупер не крал компонент. У него нет причин ни в чем признаваться. Может быть, это еще одна ловушка?
				<b>"Ну и что ещё?- Спрашивает Гаррис. <b>"Чего же ты ждешь? Пожалуйста, не говорите мне, что вы хотите признаться и сейчас, я не думаю, что моя голова выдержит это."</b>
	 				* * * [Признаться ...] 
	 						После такого шанса? Шанс — каким бы реальным он ни был — спасти мою шею? Отдать его — что, чтобы спасти никчемную шкуру Хупера?
 							* * * * [Признаться ...] 
 									Я вижу. Возможно, вы думаете, что я заставил этого человека сдаться. Возможно, он понял мою маленькую подсказку достаточно хорошо, чтобы понять, что это была угроза против него, но недостаточно хорошо, чтобы понять, где он должен искать ее. Поэтому он выбрал легкий путь и свернул. Протянул мне руку.
									 ~ hooperConfessed = true
										Было немного неуклюже, конечно.
									* * * * * 	[Признаться ...]
												Ну тогда. Я предполагаю, что это должно быть его совесть проснулась. Наверное, мне всегда было интересно.
												<b>"Гаррис, сэр. Я не знаю, во что играет Хупер, сэр. Но я не могу позволить ему сделать это."</b>
												<b>"Что делать?"</b>
												<b>"Возьми для этого веревку. Я взял его, сэр. 
												 ~ revealedhooperasculprit = false
												 ~ losttemper = false
												 -> reveal_location_of_component
									* * * * * [Не признаваться ...] 
 							* * * * [Не признаваться ...] 
	 				* * * 	[Не признаваться ...] 
	 				- - - 	<b>"Определенно нет. Но все же, я удивлен. Я обвинил Хупера в полномасштабном двойном агенте, предателе. Он знает, что ему придется столкнуться с веревкой, не так ли?"</b>
							<b>"Не проси меня объяснять, почему он сделал то, что сделал,"</b> - вздыхает Гаррис. <b>"Просто будь благодарен, что он это сделал, и теперь ты свободен."</b>
		- -		Все любопытнее и любопытнее. Я киваю Гаррису и выскальзываю наружу, на холодный утренний воздух.
				 { hooperClueType == NONE  :
					Признание Хупера имеет смысл только в одном отношении{ Hooper confessed :, и это его тупость и медлительность| — если я успешно намекнул ему, что я его подставил, но он не распаковал мою маленькую подсказку достаточно хорошо, чтобы пойти искать компонент. Ну что ж, я считал его более умным противником, но убрать его из игры будет достаточно}. Или, возможно, он знал, что за ним будут следить, если он пойдет проверить, и решил, что в любом случае он обречен.
				- else:
					Признание Хупера имеет смысл только в одном — он мне поверил. Он рассудил, что за ним будут следить. Попытка раскрыть этот компонент привела бы его к аресту, а признаться - было тем же самым. 
					Он просто сдался и отдался в руки Гарриса.
				}
				// Снаружи, вероятно освобожден
				Конечно, однако, есть только один способ убедиться, что Гаррис говорит правду, и это проверить шлакоблок в задней части Хижины 2.
			* * [Пойти проверить стену у Хижины 2 ...] -> go_to_where_component_is_hidden
			* * [Не проверяйть стену у Хижины 2 ...]
					Но для этого еще будет время. Если там ничего нет, то Хупер все-таки обнаружил компонент, и люди Гарриса набросятся на него, а история с его признанием - просто уловка, чтобы проверить меня. 
					И если компонент все еще там — хорошо. Это будет так же ценно для моего контакта через неделю, а его крайний срок 31-го числа еще не наступил.
					 -> head_for_my_dorm_free

 	* { hooperClueType == NONE } [Ждать ...] -> morning_not_saved

= morning_not_saved
	// Не был спасен
	Утро приходит с криком петуха со двора дома. Должно быть, я все-таки спал. Я поднимаюсь с койки, слегка дрожа. На внутренней стороне окна есть конденсат. Я, наверное, простудился.
	Вскоре после этого в хижину входит Гаррис. Он осторожно, как всегда, закрывает за собой дверь и садится на стул напротив меня.
	-Ты пахнешь, как собака, - замечает он.
	* (optimism) [Быть оптимистичным ...] 
	 	<b>"Я с нетерпением жду долгой ванны,- отвечаю я. <b>"И возвращаюсь к работе."</b>
	* (pessimism) [Быть пессимистичным ...] 
	 	<b>"И ты тоже, после той ночи, что у меня была."</b>

	- -> harris_certain_is_you


=== harris_certain_is_you
	<b>"Ну, я боюсь, что ситуация для тебя будет еще хуже, - серьезно отвечает Гаррис. <b>"Мы последовали за Хупером, и он аккуратно улегся в постель и заснул, как бойскаут. Что возвращает нас к вам как к главному подозреваемому. И боюсь, что у меня больше нет времени на игры в прятки с нами. Я хочу, чтобы вы сказали мне, где находится этот компонент, или мы повесим вас как предателя."</b>
	 ~ revealedhooperasculprit = false
	 ~ losttemper = false
	 -> harris_threatens_lynching




/*---------------------------------------------------------------
	Окончание: они не думают, что это был ты
---------------------------------------------------------------*/


=== head_for_my_dorm_free
Я направляюсь в свою спальню, намереваясь принять ванну, позавтракать, взглянуть на кроссворд, прежде чем другие доберутся до него, а затем продолжить работу. Они уже должны были заменить компонент в бомбе. Мы отстаём только на один день в нашей ситуации по расшифровке сообщений.
 { not framedhooper  :
	И тогда все будет продолжаться как и прежде. Этот компонент ничего не будет значить для немцев — это единственный факт, который я никогда не смог бы объяснить такому человеку, как Гаррис, хотя принцип, лежащий в основе бомбы, тот же самый, что и принцип, лежащий в основе армии. Отдельные части — люди, компоненты — не имеют значения. Они идентичны. Важно то, как они устроены. 
}
Я натыкаюсь на Рассела в общежитии. 
- Ты слышал?- он шепчет. <b>"Ужасные новости о Хупере. Совершенно невообразимо."</b>
 * [<b>"Да!"</b> ...] 
 	<b>"Просто ужасно. Я бы никогда не догадался."</b>
	<b>"Хорошо.- Рассел хмыкает. 
	- (quince) <b>"Квинс говорил сегодня утром, по-видимому, его дед был немцем. Так что, возможно, этого и следовало ожидать. Увидимся там?"</b>
 		
 * [<b>"Нет!"</b> ...]

	- Что слышал?"</b>
 	- (hooper_taken) <b>"Хупера забрали. Они поймали его, обнаружив этот недостающий компонент бомбы из тайника где-то, очевидно, собираясь передать его своему контакту.- Рассел хмыкает. -> quince
 * [Солгать ...] 
 	- Я не понимаю, о чем ты говоришь."</b> - 
 	-> hooper_taken
 * [Уклониться ...]
		- Прошу прощения, Рассел. Я как раз собирался принять ванну."</b>
		- О, конечно. Работал всю ночь, да? Ну что ж, скоро ты все узнаешь. Вряд ли можно скрыть тот факт, что отныне нас будет только трое."</b>

- Я машу ему рукой и ухожу, мои мысли возвращаются к молодому человеку в деревне. Мой любовник. Мой контакт. Мой шантажист. Хупер, возможно, взял на себя вину за недостающий компонент, но { not framedhooper :если он действительно вернул его из хижины 2, то | вернуть это означает }Мне нечего продать, чтобы спасти свою репутацию{ i_met_a_young_man :, если у меня что-то осталось}. 
 { not framedhooper  :
Если он этого не сделал, конечно, и Гаррис говорил правду о своем внезапном признании, тогда я смогу купить свою свободу раз и навсегда.
}
 * { not framedhooper } [Пойти взять компонент из тайника ...] -> go_to_where_component_is_hidden
 * { not framedhooper } [Оставить компонент в тайнике ...] 
 	Мне придется отложить этот вопрос на другой день. Вернуться туда сейчас, когда они наверняка следят за каждым моим шагом, было бы самоубийством. В конце концов, если Хупер { hooperClueType == STRAIGHT :последует|поймёт} мою подсказку, и объяснит это им, чтобы спасти свою шею. Они не поверят ему окончательно, но и не довярятся его словам. Теперь мы с ним замкнуты в круговороте полуправды и вероятности. Ни один из нас не может полностью обвинить другого.
 	-> ending_return_to_normal
 * [Вести себя нормально ...] 
 	Но с этим ничего не поделаешь. -> ending_return_to_normal




=== ending_return_to_normal
То есть ничего, кроме того, чтобы вести себя так, как будто никакой афёры не происходит. Я приму ванну, а потом начну работать как обычно. У меня есть неделя, чтобы найти что—то, чтобы дать моему шантажисту{ i_met_a_young_man: - или ничего ему не давать: похоже, мое начальство уже знает о моих неосторожностях}. 
 * [Сотрудничать ...] 
 	Что-нибудь обязательно подвернется. Так всегда бывает. Возможность представится сама собой, и теперь гораздо легче, когда Хупер убрался с дороги.
	Но на данный момент, есть вчерашнее засекреченное послание, который должно быть решено. 

 * [Скрывать ...] 
 	Или, возможно, я мог бы передать моего молодого шантажиста моему начальству вместо того, чтобы быть шпионом, которым он является.
	Возможно, это было бы даже моральнj-правильным поступком, а не только самым умным шагом. 
	Но не сегодня. Сегодня нужно кровь из носу расшифровать ставку немецких генералов.

 * [Солгать ...]
 	Через неделю все это дело останется в прошлом и будет совершенно забыто. Я в этом совершенно уверен. -> moreimportant
 * (moreimportant) [Уклониться ...] - У меня сейчас есть более важные проблемы, о которых нужно подумать. Нам еще предстоит решить проблему вчерашнего зашифрованного сообщения. 
- 	Бомбу нужно еще раз настроить и запустить. 
Пришло время заняться проблемой, которую я могу решить.
// Конец, шотландец свободен
-> END


=== go_to_where_component_is_hidden
	Это не займет много времени, чтобы решить этот вопрос сейчас. Я могу оправдать прогулку мимо Хижины 2 как часть моей утренней прогулки. Это будет очевидно через мгновение, если компонент все еще там.
	По дороге через загоны, между хижинами и домом, я замечаю юную мисс Лайон, приехавшую на работу на велосипеде. Увидев меня, она хихикает и машет рукой.
 	* [Помашите рукой в ответ ...] 
	 		Я весело машу ей в ответ, и она хихикает, чуть не роняя велосипед, а потом убегает в дом. Судя по часам на фронтоне, сегодня она немного задерживается.
 	* 	[Игнорировать ее ...] 
 			Я никак не реагирую. Она вздыхает про себя, как будто такое поведение нормально, и убегает в дом, чтобы приступить к своим обязанностям.
	- Я сворачиваю за угол хижины 3 и иду по короткой гравийной дорожке к хижине 2. Это было хорошее место для выбора — Хижина 2, где работают электрики, и они обычно сосредоточены на том, что они делают. Они не часто выходят на улицу, чтобы выкурить сигарету, так что легко проскользнуть мимо двери незамеченным.
 	* [Проверить внутри ...] 		
 			Я вскакиваю по ступенькам и все равно заглядываю внутрь. Вокруг никого. Наверное, еще слишком рано для утренних работ. <> 
 	* 	[Обойди сзади ...] 
	
	- Я направляюсь к задней части хижины. Шлакобетон с полостью находится на левой стороне.
 	* (check) [Проверить полость в шлакобетоне...] 		
 			Нельзя терять времени. Я опускаюсь на колени и проверяю полость в шлакобетоне. Конечно же, там ничего нет. <i> Хупер заглотил наживку.</i>
			Внезапно за моей спиной происходит какое-то движение. Я поднимаю глаза и вижу сначала короткосвольный револьвер, а потом Гарриса.

 	* 	[Оглядеться ...] 
 			Я останавливаюсь, чтобы оглядеться и уловить какое-то движение. Кто-то нырнул за угол хижины. Или полотняная простыня, колышущаяся на легком ветерке. Невозможно быть уверенным до конца.
  			* * [Проверьте шлакобетон ...] -> check
 			* * [Проверить территорию вокруг хижины ...] 
 						Но важнее знать, чем угадать. Я двигаюсь за угол хижины.
						Там стоит Гаррис, прислонившись к стене. В руке он держит короткоствольный пистолет.

	- { hooperClueType > STRAIGHT  :
			<b>"{ hooperClueType == CHESS:Ферзь на ладью два|Грязно без одного пропущенного, что бы это ни было}"</b>, - заявляет он. <b>"Я бы этого не понял, но Хупер понял. Объяснил это сразу после того, как мы заставили его делать то, что ты делаешь сейчас. Мы не были уверены, чему верить, но теперь вы, кажется, решили это за нас."</b>
		- else:
			 Хупер сказал, что вы сказали ему, где искать. Я ему не поверил. Или точнее я не знал, чему верить. А теперь я думаю, что вы все уладили."</b>
		}
	 * 	[Согласиться ...] 
		 	<b>"Вернее, видел."</b> - Я сунул руки в карманы. <b>"Кажется, именно это я и сделал."</b>
			<b>"Боюсь, что моя маленькая история о том, как Хупер признался, была неправдой. Я хотел посмотреть, пойдешь ли ты за этой частью."</b> - Гаррис жестом велит мне идти. <b>"Ты был близок к этому, Мэннинг, надо отдать тебе должное. Я хотел тебе верить. Но я рад, что этого не сделала."</b>
			-> done
	 * 	[Солгать ...] 
		 	<b>"Я говорил с Расселом. Он сказал, что видел, как Хупер что-то здесь делает. Я хотел посмотреть, что это было."</b>

	 * 	[Уклониться ...] 
		 	<b>"Гаррис, тебе лучше быть поосторожнее. Он подложил сюда бомбу замедленного действия."</b>
			Гаррис пристально смотрит на меня, потом смеется. <b>"О боже! Это очень шедро!"</b>
			Я почти жалею, что у меня нет способа заставить хижину взорваться, и конечно, я этого не делаю.

	- 	<b>"Достаточно."</b> - Гаррис жестом велит мне идти. <b>"Эта история не могла быть ещё проще. Ты взял компонент, чтобы прикрыть спину. Ты его спрятал. Вы солгали мне, чтобы втянуть Хупера в неприятности, и когда вы думали, что выиграли, вы пришли, чтобы забрать свой приз. Хорошая комбинация, но в конечном счете, {hooperClueType <= STRAIGHT :если бы не вы спрятали компонент, то вас бы сейчас здесь не было|вы сказали Хуперу, где искать свою маленькую загадку}."</b>

 	- (done) 
	// Конец - Пойман утром
		Он ведет меня через двор. Назад к хижине 5, чтобы ее расшифровали и снова разобрали на куски.
		-> END


/*---------------------------------------------------------------
	Концовка: они думают, что это был ты
---------------------------------------------------------------*/

=== harris_threatens_lynching
 	{ harris_certain_is_you:Он проводит рукой по векам с долгим выражением отчаяния.|Он поднимается на ноги, и берёт свои перчатки со стола.}
	<b>"Я пойду на улицу и приготовлю веревку. Это займет около двенадцати минут. Вот как долго вы должны решать."</b>
	 * [Возразить ...] 
	 	<b>"Вы не можете так поступить!"</b> - кричу я. <b>"Это убийство! Я требую суда, адвоката; ради бога, парень, ты не можешь просто выбросить меня за борт, мы же не варвары...!"</b>
		- - (too_clever) <b>"Ты не оставляешь мне выбора,"</b> - огрызается Гаррис, его глаза холодны, как металл пистолета. <b>"Ты и твои чертовы шифровки. Твои чертовски умные головоломки. Если бы таких людей, как ты, не существовало, то мы все могли бы быть честными друг с другом."</b> - Он встает на ноги и направляется к двери. <b>"Но я боюсь за будущее этого мира, где живут такие люди, как ты. Живя в Рейхе или нет, Мистер Мэннинг, но такие люди, как вы, просто усложняют дело."</b>
		 -> left_alone
	 * { not gotcomponent && not throwncomponentaway } [Сознаться ...] 
	 		Я киваю. <b>"Мне не нужны эти двенадцать минут!"</b> -> reveal_location_of_component
	 * [Молчать ...] -> my_lips_are_sealed
	 * { gotcomponent } [Показать ему компонент ...] 
	 		<b>"Мне не нужны двенадцать минут. Вот он. Смотрите<b>"
			Я расстегиваю куртку и достаю из кармана бомбу. Гаррис берет ее у меня, насвистывая, с любопытством.
			<b>"Ну что ж, буду. Теперь всё в порядке."</b>
			<b>"Вот именно."</b>
			<b>"Но вчера у тебя его не было."</b>
			* * [Объяснить ...] 
			 	<b>"Я вылез из окна ночью,"</b> - объясняю я. <b>"Я пошел и взял это оттуда, где оно было спрятано, и принес сюда."</b>
			* * [Не объясняй ...]
				<b>"Нет. Я промолчу."</b>
			- -> all_too_farfetched

	 * { throwncomponentaway } [Сознаться ...]
		 	<b>"Мне не нужно двенадцать минут. Компонент находится в высокой траве позади палатки Хупера. Я бросил его туда, надеясь как-то подставить его, но теперь вижу, что это невозможно. Наверное, я был наивен."</b>
			 ~ piecereturned = true
			 -> reveal_location_of_component.harris_believes

	 * { throwncomponentaway } [Подставляя Хупера...] 
		 	<b>"Послушай, я знаю, где это. Недостающая часть бомбы лежит в высокой траве позади палатки Хупера. Я видел, как он бросил его туда сразу после того, как мы закончили работу. Он знал, что Вы будете рыскать по лагерю, но считаю, он думал, что сначала найдёте её Вы в более очевидных местах. Наверное, он был прав. Смотреть туда. Это <i>доказывает</i> его вину."</b>
			 ~ longgrasshooperframe = true
			 ~ piecereturned = true
			<b>"Это ничего не доказывает!"</b> - резко отвечает Гаррис. <b>"Но мы все равно проверим всё то, что ты скажешь."</b> - Он встает на ноги и направляется к двери.
			 -> left_alone



=== reveal_location_of_component
	Недостающий компонент компьютера Bombe спрятан в небольшой полости шлакобетона, поддерживающем левую заднюю стойку Хижины 2. Я положил его туда ожидая обыска. Я намеревался { revealedhooperasculprit:передать его Хуперу|избавиться от него}, как только суета утихнет. Наверное, я был глуп, думая, что это возможно."</b>
	~ piecereturned = true
	-> harris_believes	
= harris_believes
 	{ not night_falls.hooper_didnt_give_himself_up  :
		<b>"Действительно. И Мистер Мэннинг: да поможет вам Бог, если вы мне лжете."</b> - 
	- else:
		<b>"Я так и думал. Однако я не ожидал, что ты так легко его выдашь. Вы, конечно, понимаете, что Хупер ничего не сказал. Фактически, он отправился в Хижину 2 сразу после того, как мы освободили его и обнаружили компонент. Но он сказал нам, что вы проинструктировали его, куда идти. Отсюда и мой двойной блеф. Откровенно говоря, я буду рад, когда меня расстреляют из вас, математиков."</b>
	}
	Гаррис встает и ловко ускользает. -> left_alone



=== my_lips_are_sealed
	Я ничего не говорю, мои губы сомкнуты. Это правда, что я предатель, нарушивший все законы мироздания. Мир научил меня этому с самого раннего возраста. Но не для моей страны — если Рейх выиграет эту войну, меня вряд ли будут считать заслуженным героем. Я был обречен с самого начала.
	 ~ notraitor = true
	Я ничего не буду объяснять. Как такой человек, как Гаррис сможет понять мои мотивы?
	Командир бросает один взгляд назад от двери, когда он тянет ее к себе.
	<b>"Мне было очень приятно работать с вами, мистер Мэннинг,"</b> - заявляет он. <b>"Вы оказали огромную услугу этой стране. Если мы переживём это, то я уверен, что они запомнят твое имя. Мне жаль, что все так закончилось, и я сделаю все возможное, чтобы это осталось в тайне. Никто не должен знать, что ты сделал."</b>
	 -> left_alone




=== all_too_farfetched
	// Компонент Вернули на Место
	<b>"Все это слишком притянуто за уши,"</b> - говорит Гаррис. <b>"Я рад, что вс вернулось на место, но мне ещё нужно подумать."</b>
	Встав на ноги, он кивает один раз. <b>"Боюсь, вам придется еще немного подождать, Мэннинг."</b>
	Затем он выходит за дверь, бормоча что-то себе под нос.
	 -> make_your_peace



=== left_alone
	// Умереть в одиночестве
	{ slam_door_shut_and_gone.time_to_move_now : Командир придерживает дверь для своего начальника и следует за ним.} Затем дверь закрывается. Я снова один, как и большую часть своей короткой жизни.
	 -> make_your_peace


=== make_your_peace
	* [Сделай свой мир ...]
	- Я опять жду. У меня нет Бога, с которым я мог бы помириться. Мне трудно поверить в доброту любого рода в таком мире, как этот. 
 		{ not notraitor:
 			~ notraitor = true
			Но я не предатель. Только не для моей страны. Возможно, для мужчин. Но как я мог поддерживать Рейх? Если нацисты придут к власти, мне будет хуже, чем когда-либо прежде.
		}	
 		{ harris_threatens_lynching.too_clever:
			По правде говоря, именно такие люди, как Гаррис, являются сложными, а не такие, как я. Я живу, чтобы делать вещи упорядоченными, систематичными. Я люблю, чтобы мои карандаши были заточены и выстроены в ряд. Я не имею дела с трудными границами, неопределенностью или альянсами. Если бы я мог, я бы свел мир к чему-то более понятному, к чему-то конечному. 
			Но я не могу, даже здесь, в нашем маленьком убежище спрятаться от ужасов войны.
		}
		Мне здесь не место. Я в ловушке, в середине, между шифрами и машинами которые понятны только мне. 
 			* 	Я должен казаться очень спокойным. 			
 			*	Возможно, мне следует попытаться сбежать.[] Но куда? Я уже в плену. Тюрьма была бы благословением. -> monastic
	- 	<> Наверное, я не верю, что меня повесят. Они запрут меня и продолжат использовать мой мозг, если смогут. Интересно, что они скажут миру — возможно, что я покончил с собой. Это было бы проще всего. Те немногие, кто меня знает, поверят в это.
		Ну тогда. Не такое уж и плохо существование в тюрьме. Лишен соблазнов. 
	- (monastic) Монашеская жизнь, с большим количеством проблем, чтобы поддерживать меня в тонусе. 
		Интересно, что еще я могу разгадать, прежде чем наступит конец?
 			* Дверь открывается.[] Гаррис возвращается. Наш маленький расчет здесь завершен. { not piecereturned: Я могу только надеяться, что кто-нибудь другой сумеет объяснить ему, что часть, которую я украл, ничего не будет значить для немцев.|Мы просто части в этой машине; взаимозаменяемы и подвержены износу.}
	- 	В этом и есть истинный секрет вычислительной машины и источник ее мощности. Это не компоненты, которые имеют значение, они довольно однотипны. Важно то, как они связаны; разнообразие паттернов и структур, которые они могут формировать. Очень похоже на людей — именно то, как они соединяются, определяет наши победы и трагедии, а не их гениальность.
		Что заставляет меня задуматься. Должен ли я отдать { i_met_a_young_man : моего любовника|молодого человека, из-за которого я здесь} им также, как и себе?
		 * 	[<b>"Да!"</b> ...] 
		 		Но, конечно, я это сделаю. { forceful > 2:Возможно, я смогу убедить поместить его в мою камеру.|Небольшая месть, замаскированная под доброе дело.}
		 * 	[<b>"Нет!"</b> ...] 
		 		Нет. Какая от этого польза? Его уже давно не будет, и имя, которое он мне назвал, без сомнения, фальшивка. Нет, прежде я был одинок в чувстве вины, и теперь я снова одинок.
		 * 	[Солгать ...] 
		 		Нет. С чего бы это? Он не виновен, и попав в ловушку таких ужасных обстоятельств, вынужденный действовать так же, как и я. Я ему очень сочувствую.
				 
		 * 	[Уклониться ...] 
		 		Возможно, это зависит от того, чего стоит его имя. Если бы это оказалось ценным, то возможно я сумею состряпать еще несколько таких любовников, чтобы облегчить свои последние дни.
				{ hooper_mentioned: Хупер, возможно. Но ему бы это не понравилось. }
	- { not longgrasshooperframe  :
			Гаррис надел наручники на мои запястья. <b>"У меня в кармане все еще лежит перехваченная шифровка,"</b> - замечаю я. <b>"Куда бы мы ни направлялись, можно мне взять карандаш?"</b>
		- else:
			<b>"Мы нашли ту часть, где ты сказал,"</b> - сообщает Гаррис, надевая наручники на мои запястья. <b>"Конечно, двое мужчин клянутся, что вчера обыскивали вспешке, так что я боюсь, что это из-за разбитого окна... мы разработали отличную теорию, которая не сулит вам ничего хорошего."</b>
		}
	 	~ piecereturned = true
		{ longgrasshooperframe  :
		<b>"Понимаю."</b> - Кажется, дальше спорить не стоит. <b>"У меня в кармане все еще лежит перехваченная шифровка,"</b> - замечаю я. <b>"Куда бы мы ни направлялись, можно мне взять карандаш?"</b>
		}
		Он смотрит мне прямо в глаза.
		{ not losttemper  :
			<b>"Конечно. И одна из твоих компьютерных штучек, если я добьюсь своего. А когда мы состаримся и будем вместе курить трубки в тряпье, как герои, я объясню тебе, как порядочные люди заводят романы. 
		- else:
			<b>"Я дам тебе камень, чтобы высечь зарубки на стене. И это все расчеты, которые вы будете делать. И пока вы сидите там, мочитесь в ведро и отращиваете бороду до кончиков пальцев ног, вы думаете о том, как <i>умный</i> человек будет вести свои незаконные дела. С небольшой долей должного этикета вы могли бы поучиться у любого эскадрилья.
		}
		<> Вы ученые."</b> - 
		Он поднимает меня на ноги. 
		<b>"Ты думаешь, что должен все заново изобрести."</b>
		С этими словами он выпроваживает меня за дверь, и я не могу отделаться от мысли, что, будь у меня чуть больше стратегии, я все еще могла бы выиграть день. Но теперь, конечно, уже поздно.
		-> END